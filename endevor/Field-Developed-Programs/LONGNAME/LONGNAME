/*  REXX  */                                                                    
/* THESE ROUTINES ARE DISTRIBUTED BY THE CA TECHNOLOGIES STAFF                  
   "AS IS".  NO WARRANTY, EITHER EXPRESSED OR IMPLIED, IS MADE                  
   FOR THEM.  CA TECHNOLOGIES CANNOT GUARANTEE THAT THE ROUTINES                
   ARE ERROR FREE, OR THAT IF ERRORS ARE FOUND, THEY WILL BE                    
   CORRECTED.                                                                   
*/                                                                              
                                                                                
   /* WRITTEN BY DAN WALTHER                                                    
      Extended By Eoin O'Cleirigh */                                            
   TRACE o                                                                      
                                                                                
   ADDRESS ISPEXEC "VGET (ZAPPLID ZSCREEN zUSER zSYSID zPREFIX)"                
   LONGTABL = "LN" || ZSCREEN || "IE250"    /* set Table name used */           
   LONGPANL = "LONGLIST"                    /* Default to pri scrn */           
   LONGTABS = "SUM" || ZSCREEN || "LVL"     /* Summary Table name  */           
   LONGPANS = "SUMLEVLS"                    /* Summary Pri Screen  */           
   LONGDDPR = "LN" || ZSCREEN               /* set DD prefix       */           
   REFRESHR = "NO"                          /* initialise Refresh  */           
   LNLASTGT = "#"                           /* if no tgt env set   */           
   LNLASPRM = ""                            /* No actions proc yet */           
   LNLASTYP = ""                            /* No Types proc yet   */           
   EMULTGT  = ""                            /* default tgt         */           
   EMULPRM  = ""                            /* and promote         */           
   EMULTYPP = ""                            /* and no types profile*/           
   EMULTYPS = ""                            /* ... no types shared */           
   ADDRESS ISPEXEC "VGET (SUBHIST) SHARED"  /* Check to see if we  */           
   if SUBHIST = '' then                     /* have any current, or*/           
      SUBHIST  = "***"                      /* initialise history  */           
   ZCMD = ""                                /* Default command     */           
   QEMsg = ""                               /* Initialise message  */           
   ENV_NAME.0 = 0                           /* Initialise no. Env  */           
   TypeLen.   = ""                          /* Initialise TypeLen  */           
   TypeRFM.   = ""                          /* Initialise TypeRecfm*/           
                                                                                
   /* Decide on Temporary Dataset name prefix...                         */     
   if zSYSID = SPECIAL then do              /* is this a special system? */     
      /* insert system specific logic if required here                   */     
      LongDsPrefix = left(zUSER,3)||'.'|| ,                                     
         zUser'.'STRIP(LEFT('E'||ZSYSID,8))||'.LONG'||ZSCREEN                   
      LongTempDir  = '/tmp/'left(zUSER,3)||'/'|| ,                              
         STRIP(LEFT('E'||ZSYSID,8))||'/LONG'||ZSCREEN                           
      end                                                                       
   else /* otherwise we use some sensible defautls                       */     
     if zPrefix \= '',                      /* is Prefix set?  and NOT.. */     
      & zPrefix \= zUSER then do            /* the same as userid?       */     
        LongDsPrefix = zPrefix ||'.'|| ,                                        
           zUser'.'STRIP(LEFT('E'||ZSYSID,8)) || '.LONG'||ZSCREEN               
        LongTempDir  = '/tmp/'zPrefix||'/'||zUser||'/'|| ,                      
           STRIP(LEFT('E'||ZSYSID,8))||'/LONG'||ZSCREEN                         
        end                                                                     
     else do                                /* otherwise use user name   */     
        LongDsPrefix = zUser ||'.'|| ,                                          
           STRIP(LEFT('E'||ZSYSID,8)) || '.LONG' || ZSCREEN                     
        LongTempDir  = '/tmp/'||zUser||'/'|| ,                                  
           STRIP(LEFT('E'||ZSYSID,8))||'/LONG'||ZSCREEN                         
     end                                                                        
                                                                                
   IF ZAPPLID /= 'CTLI' THEN,                                                   
      DO                                                                        
      ADDRESS ISPEXEC,                                                          
        "SELECT CMD(LONGNAME) NEWAPPL(CTLI) PASSLIB"                            
      EXIT                                                                      
      END;                                                                      
                                                                                
/* Hiding errors is making debug difficult, better to let it crash              
   disabling the next two lines - Eoin                                          
   "ISPEXEC CONTROL RETURN ERRORS"                                              
   "PROFILE NOWTPMSG"                                                           
   */                                                                           
                                                                                
/* Client options can be coded in this section  --->                */          
/*                                                                  */          
/* The LongName utility will use a LIST TYPE CSV call to determine  */          
/* the correct record length for a temporary file, but this is a    */          
/* relatively expensive operation, so even though the results are   */          
/* cached, the facility also exists to provide override or hard-    */          
/* coded values instead.                                            */          
/*                                                                  */          
/* For instructions on how to tailor this value see below;          */          
/*                                                                  */          
/*   Create a type table for those types that you want to FORCE     */          
/*   or override the record length.                                 */          
/*                                                                  */          
/*   Then create a table of LRECL/BLKSIZE lengths for each type     */          
/*   in the type table. The LRECL must match the type length        */          
/*   on the Endevor Type definition (or if the Recfm is VB, add     */          
/*   4 for the Record Length field). The BLKSIZE value can be       */          
/*   any reaonable value for use by a temporary file, or zero       */          
/*   to let the system decide.                                      */          
/*   Then add a table of Record Format options, for example:        */          
/*      FB, VB, FBA, VBA, etc.                                      */          
/*                                                                  */          
     Non_80byte_Types =,                                                        
      "NATURAL  OTHER CPGM  CSV     CSVG    COB  COPY"                          
     Non_80byte_Lengths =,                                                      
      "90/27990 100/0 255/0 32004/0 32004/0 80/0 80/0"                          
     Non_80byte_Recfms =,                                                       
      "FB       FB    VB    VB      VB      FB   FB  "                          
/*                                                                  */          
/* Now we'd like to know what defatuls to load, get the map etc.    */          
DFCUNAME = ''                                /* Flag not loaded     */          
DFSITEID = '0'                               /* Default SiteID      */          
ExPrm = 'C1DEFLTS' || left(userid(),8)       /* Set up Parm for call*/          
ADDRESS LINKPGM 'ENUXSITE ExPrm'             /* Call ENUXSITE       */          
ExRC = RC                                    /* save Exit RC        */          
if ExRC \= 0 then do                         /* nonzero RC from exit*/          
   sa= "ENUXSITE RC:" ExRC "ExPrm:" ExPrm    /* warn User           */          
   DFCUNAME = "CUNAME unknown, Check ENUXSITE" /* and set message   */          
end                                                                             
else do                                      /* otherwise all OK... */          
   DFTABNAM = LEFT(ExPrm,8)                  /* use returned value  */          
   ADDRESS LINKMVS 'LOADTABL' DFTABNAM       /* Try to load table   */          
   select                                                                       
      when RC = 0 then nop                   /* all OK - get out    */          
      when RC = -3 then do                                                      
         say "LOADTABL routine not found, please contact your Endevor Admin"    
            DFCUNAME = "CUNAME unknown, Check LOADTABL"                         
         end                                                                    
      when RC = -2054 then do                                                   
         say "Defaults table("DFTABNAM") not found, contact your Endevor Admin" 
            say "Defaults table(C1DEFLTS) not found, please" ,                  
                "contact your Endevor Admin"                                    
            DFCUNAME = "CUNAME unknown, Check" DFTABNAM                         
         end                                                                    
      otherwise do                                                              
         Say "Unexpected RC:" RC,                                               
         "from LOADTABL routine, Please contact your Endevor Administrator"     
            DFCUNAME = "CUNAME unknown, Check LOADTABL/C1DEFLTS"                
         end                                                                    
   end                                                                          
   if RC \= 0 then                                                              
      DFCUNAME = "CUNAME unknown, Check ENUXSITE/" || DFTABNAM                  
   else do                                                                      
      C1DEFLTS_ADDR = C2X(TBLADDR)                                              
      DFCUNAME = strip(STORAGE(C1DEFLTS_ADDR,50))                               
      POINTER_ADDR = D2X(X2D(C1DEFLTS_ADDR) + X2D(092))                         
      DFSITEID = STORAGE(POINTER_ADDR,01)                                       
      call Find_Env_map    /* carry on and get rest of env map */               
   end                                                                          
end                                                                             
/*                                                                  */          
/* Main Panel Logic Starts here                                     */          
/*                                                                  */          
   FLNam = left('E Q=FDP-LONG IS ACTIVE IN SCREEN' ZSCREEN,34)                  
   ADDRESS LINKMVS 'CTLIENQ FLNam'                                              
   if RC\= 0 then do                      /* LongName Already Active */         
      /* ENDI002E The dialog is already active in this ISPF logical..*/         
      ADDRESS ISPEXEC "SETMSG MSG(ENDI002E)" ; /* Use Endevor msg    */         
      exit                                                                      
   end                                                                          
                                                                                
   Do Forever                                                                   
      /* bypass panel display IF a refresh has been requested       */          
      if REFRESHR == 'NO' THEN DO                                               
         ADDRESS ISPEXEC "DISPLAY PANEL(ENDIE11L) "                             
         Panel_RC = RC;                                                         
         THISCMD = VARWKCMD ;                                                   
         VARWKCMD = "";                                                         
         If Panel_RC > 0 then leave                                             
         THISCMD = translate(THISCMD) /*uppercase*/                             
         If THISCMD = 'NOP' then iterate;                                       
         If THISCMD = 'CR' then DO                                              
            Call Create_New_Element;                                            
            iterate;                                                            
         end                                                                    
      End                                                                       
      else                                                                      
         REFRESHR = "NO"                     /* Reset Refresh Reqest*/          
                                                                                
Build_Element_list:                                                             
      C1ENVMNT =  QEPEVNME                                                      
      C1SYSTEM =  QEPSYS                                                        
      C1SUBSYS =  LNPSBS                                                        
      EEVETKEL =  QEPELML                                                       
      C1ELTYPE =  QEPTYP                                                        
      ESRFOUND =  ' ' /* reset found flag */                                    
                                                                                
      if C1ENVMNT = ' ' then C1ENVMNT = "*" ;                                   
      if C1SYSTEM = ' ' then C1SYSTEM = "*" ;                                   
      if C1SUBSYS = ' ' then C1SUBSYS = "*" ;                                   
      if EEVETKEL = ' ' then EEVETKEL = "*" ;                                   
      if C1ELTYPE = ' ' then C1ELTYPE = "*" ;                                   
                                                                                
      if ENV_NAME.0 > 0 then do             /* if we have a valid defaults? */  
         Env_OK = 'N'                                                           
         do i = 1 to ENV_NAME.0                                                 
            if C1ENVMNT == ENV_NAME.i then do                                   
               Env_OK = 'Y'                                                     
               leave                                                            
            end                                                                 
         end                                                                    
                                                                                
         if Env_OK = 'N' then do               /* Invalid environment name? */  
            VAREVNME = C1ENVMNT                    /* store var for message */  
            ADDRESS ISPEXEC "SETMSG MSG(ENDE004E)" ; /* Envir not def/auth  */  
            iterate                          /* go back and display message */  
         end                                                                    
      end                                                                       
                                                                                
      SA= "Check if Apply List Filters"                                         
      ADDRESS ISPEXEC "VGET (EEVALF EEVSRCHM EEVFFND) PROFILE "                 
      if EEVALF == 'Y' then do /* user did request to apply list filters */     
         ADDRESS ISPEXEC "VGET (EEVWCUT1 EEVWCUT2 EEVWCUT3 EEVWCUT4",           
                               "EEVWCCID EEVWUID EEVWPGRP EEVALF) PROFILE"      
         address ISPEXEC "display panel(ENDIE14L)" /* display prompt panel*/    
         promptRC = RC                                                          
         if PromptRC >= 8 then iterate /* user quit from panel redisplay */     
         Sa= 'CCID...........:'EEVWCCID                                         
         sa= 'Userid.........:'EEVWUID                                          
         Sa= 'Procerror Group:'EEVWPGRP                                         
         Sa= 'UseListFilters.:'EEVALF                                           
         Sa= 'CCID options...:'EEVWCUT1 EEVWCUT2 EEVWCUT3 EEVWCUT4              
         Sa= 'Backout Option.:'LNFILBK                                          
         Sa= 'Locked Option..:'LNFILLK                                          
         Sa= 'Stage Filters..:'LNFILST                                          
      end                                                                       
                                                                                
      SA= "CALL API_to List_Elements"                                           
      CALL API_to_List_Elements;                                                
         /* Also allocates files for API output */                              
                                                                                
      IF APIRC   >= 0 then            /* Were there any errors?            */   
         QEMsg=FindMsg(C1MSGSA,APIRC) /* Find Warning message              */   
      IF APIRC = 4,                   /* If warnings Issued...             */   
       & MULTREQ > 1 then             /* and we had multiple requests      */   
           ADDRESS ISPEXEC "SETMSG MSG(LONG011I)" ; /* some patters failed */   
      else do                                                                   
         IF APIRC    = 4  then do      /* Warning on list - no match       */   
            IF QEMsg = '' then do      /* Not a recognized message?        */   
               ADDRESS ISPEXEC "SETMSG MSG(LONG011W)" ; /* List_RC warn    */   
            /* ADDRESS ISPEXEC "LMINIT DATAID(DDMA) DDNAME(C1MSGSA)"            
               VARC1LR = ''            /* Allow standard Left/Right        */   
               ADDRESS ISPEXEC "VIEW   DATAID(&DDMA) PANEL(ISREDDHI)",          
                                                    "MACRO(ENDVHISC)"           
               VARC1LR = PASSTHRU      /* enable Left/Right commands       */   
               ADDRESS ISPEXEC "LMFREE DATAID(&DDMA)"  */                       
            end                                                                 
            else                                                                
               ADDRESS ISPEXEC "SETMSG MSG("QEMSG")" ; /* The Found Msg    */   
            iterate ;                                                           
         end                                                                    
      end                                                                       
                                                                                
      IF APIRC  >= 12,              /* Error on list? */                        
       | APIRC  <  0  THEN Do       /* -3 mod not found, 47 not auth */         
         ADDRESS ISPEXEC "SETMSG MSG(LONG012E)" ; /* List_RC high */            
         iterate ;                                                              
      end                                                                       
                                                                                
      CALL Load_ISPF_Table_For_Display ;                                        
         /* Also Frees     files for API output */                              
                                                                                
      IF Unique_Ele# < 1 then do       /* If no elements in list and       */   
         IF QEMsg = '' then            /* Not a recognized message?        */   
            ADDRESS ISPEXEC "SETMSG MSG(LONG011E)" ; /* No Eles match      */   
         else                          /* Not a recognized message?        */   
            ADDRESS ISPEXEC "SETMSG MSG("QEMSG")" ; /* The Found Msg       */   
         iterate ;                                                              
      end                                                                       
                                                                                
      CALL Display_Table ;                                                      
                                                                                
   End ; /* Do Forever */                                                       
                                                                                
   /* If we fall out of the main loop - we must be finished                     
      consider adding code to free up any datasets that                         
      might be left lying around, and CLose API if still running...             
      */                                                                        
                                                                                
Clean_Up:                                                                       
   /* Check for un-submitted requests*/                                         
   ADDRESS ISPEXEC "VGET (EMULTREQ EMULTGT EMULPRM EMULTYPP) PROFILE"           
   ENABAUTO = 'N'                            /* TurnOff auto submit         */  
   ADDRESS ISPEXEC "VPUT (ENABAUTO) Shared"  /* and save for LONGSUBJ       */  
   if EMULTREQ > 0 then do                                                      
      ADDRESS ISPEXEC "SETMSG MSG(LONG020W)" ; /* Pending reqs!*/               
      sa=SAVESCL(RESET)                      /* Clear SCL (just in case)    */  
      call Show_JobCard                      /* Last Chance to submit?      */  
      end                                                                       
                                                                                
   /* delete any preexisting files */                                           
                                                                                
   CALL OUTTRAP "out."                                                          
      "FREE  F("LONGDDPR"BROWS)"                                                
      "FREE  F("LONGDDPR"CSVD)"                                                 
      "DELETE '"LongDsPrefix".CSVD'"                                            
      "FREE  F(C1MSGS1)"                                                        
      "DELETE '"LongDsPrefix".MSG1'"                                            
      "FREE  F(C1MSGSA)"                                                        
      "DELETE '"LongDsPrefix".MSGA'"                                            
   CALL OUTTRAP "OFF"                                                           
                                                                                
   /* Release Active in Screen Enq */                                           
                                                                                
   FLNam= 'D' || Substr(FLNam,2)   /* change to DeQueue request     */          
   ADDRESS LINKMVS 'CTLIENQ FLNam'                                              
                                                                                
   EXIT                                                                         
                                                                                
API_to_List_Elements:                                                           
                                                                                
   SA= "API_to_List_Elements:        "                                          
   SA= "GETTING CURRENT LOCATIONS FROM ENDEVOR" ;                               
   ADDRESS TSO;                                                                 
   /* delete any preexisting files */                                           
   CALL OUTTRAP "out."                                                          
      "FREE  F("LONGDDPR"CSVD)"                                                 
      "DELETE '"LongDsPrefix".CSVD'"                                            
      "FREE  F(C1MSGS1)"                                                        
      "DELETE '"LongDsPrefix".MSG1'"                                            
      "FREE  F(C1MSGSA)"                                                        
      "DELETE '"LongDsPrefix".MSGA'"                                            
   CALL OUTTRAP "OFF"                                                           
                                                                                
   "ALLOC F(C1MSGS1) LRECL(133) BLKSIZE(26600) SPACE(5,5) ",                    
   "DA('"LongDsPrefix".MSG1')",                                                 
    "DSORG(PS)",                                                                
     "RECFM(V B) TRACKS MOD CATALOG REUSE " ;                                   
                                                                                
   "ALLOC F(C1MSGSA) LRECL(133) BLKSIZE(26600) SPACE(5,5) ",                    
   "DA('"LongDsPrefix".MSGA')",                                                 
    "DSORG(PS)",                                                                
     "RECFM(V B) TRACKS mod CATALOG REUSE " ;                                   
/* ADDRESS TSO "ALLOC FI(C1MSGS2)  DUMMY SHR REUSE" */                          
/* 'ALLOC F(BSTERR)   DUMMY SHR REUSE '  */                                     
/* 'ALLOC F(BSTAPI)   DUMMY SHR REUSE '  */                                     
                                                                                
   "ALLOC F("LONGDDPR"CSVD) LRECL(4096) BLKSIZE(0) SPACE(5,10) ",               
   "DA('"LongDsPrefix".CSVD')",                                                 
    "DSORG(PS)",                                                                
     "RECFM(V B) CYL MOD CATALOG REUSE " ;                                      
                                                                                
   "ALLOC FI(BSTIPT01) BLKSIZE(0) TRACKS LRECL(80) SPACE(5 5)",                 
    "DSORG(PS)",                                                                
       "RECFM(F B) NEW REUSE UNCATALOG" ;                                       
                                                                                
                                                                                
   sa=SAVESCL(RESET)                    /* reset our cache of SCL lines */      
   /* check our subsys name - treating commas as spaces */                      
   SUBNAMS = translate(STRIP(C1SUBSYS),' ',',')                                 
   /* Note: Spaces are now valid in element names, so only accept commas */     
   ELENAMS = STRIP(EEVETKEL)                                                    
   MULTREQ = 0                                                                  
   do i = 1 to cwords(ELENAMS)                                                  
      do j = 1 to words(SUBNAMS)                                                
         multreq = multreq + 1         /* another request */                    
         sa=SAVESCL(" LIST ELEMENT ")                                           
         sa=SAVESCL("'"cword(elenams,i)"'")                                     
         sa=SAVESCL("  FROM ENV" C1ENVMNT,                                      
                         "SYS" C1SYSTEM,                                        
                         "SUB" word(subnams,j),                                 
                         "TYP" C1ELTYPE)                                        
         select                                                                 
            WHEN EEVSRCHM == "1" THEN sa=SAVESCL("     STAGE NUMBER '1'")       
            WHEN EEVSRCHM == "2" THEN sa=SAVESCL("     STAGE NUMBER '2'")       
            OTHERWISE                 sa=SAVESCL("     STAGE NUMBER '*'")       
         end                                                                    
      /* sa=SAVESCL("  DATA BASIC         ")*/                                  
         Options = " "                                                          
         IF EEVSRCHM == "Y" THEN Options = " SEARCH " ;                         
         IF EEVALF   == "Y" THEN do /* do we have search filters? */            
           WhereOpt = '' /* initialise Where opt */                             
           If EEVWCCID /= '' then do       /* CCID was specified? */            
             whereOpt = 'CCID'                                                  
             if EEVWCUT1 = 'Y' then whereOpt = whereOpt 'OF CUR' ;              
             else if EEVWCUT2 = 'Y' then whereOpt = whereOpt 'OF GEN' ;         
               else if EEVWCUT3 = 'Y' then whereOpt = whereOpt 'OF LAST ACT' ;  
                 else if EEVWCUT4 = 'Y' then whereOpt = whereOpt 'OF RET' ;     
             whereOpt = whereOpt '= ('EEVWCCID')'                               
           end                                                                  
           /*                                                                   
             LIST ELE does not support where UserID - so should I               
             filter it when parsing/adding elements? or wait for support?       
           If EEVWUID  /= '' then          /* UserID was specified? */          
             whereOpt = whereOpt 'USERID = ('EEVWUID')'                         
             */                                                                 
           If EEVWPGRP /= '' then          /* Processor Group Spec? */          
             whereOpt = whereOpt 'PROC GRO = ('EEVWPGRP')'                      
           if WhereOpt /= '' then          /* anything to filter? */            
              sa=SAVESCL("  WHERE" WhereOpt)                                    
         end                                                                    
         /* Don't support wildcard Env yet, we wouldn't know where to add       
         IF INDEX(C1ENVMNT,'*') > 0 , /* is Environment WildCard? */            
          | INDEX(C1ENVMNT,'%') > 0 then  Options = " NOSEARCH " ;              
         */                                                                     
         IF EEVFFND   = "Y" THEN Options = Options "RETURN FIRST" ;             
         ELSE                    Options = Options "RETURN ALL" ;               
         sa=SAVESCL("  TO DDNAME '"LONGDDPR"CSVD'",                             
                    "OPTIONS" Options "DELIMITERS 'º' .")                       
      end                                                                       
   end                                                                          
                                                                                
   sa=SAVESCL(EXECIO, BSTIPT01)         /* write our cache to temp file */      
                                                                                
   /*                                                                  */       
   /* Now call the Endevor CSV utility to find all the matching        */       
   /* elements - note a return code of -3 means the module wasn't      */       
   /* found - add a "libdef islpplib dataset id(...conlib) stack"      */       
   /*                                                                  */       
   ADDRESS LINK 'BC1PCSV0'   ;  /* Try load from authlib or linklist?  */       
   APIRC = rc ;                 /* save the API response code          */       
   If apirc = -3 then do        /* if module not found in STEPLIB or   */       
                                /* ISPLLIB then try loading from conlib*/       
      ndvrprm = 'CONCALL,DDN:CONLIB,BC1PCSV0'   /* set up parm for CSV */       
      address TSO "call *(NDVRC1)  '"ndvrprm"'" /* need to use TSO for */       
      APIRC = rc ;                              /* auth pgm call       */       
   end                                                                          
   if apirc <  0,               /* Negative RC means something bad     */       
    | apirc > 12 then           /* but 4, 8 and 12 are normal          */       
      Sa= "Unexpected RC:" apirc,                                               
      "from BC1PCSV0 routine, Please contact your Endevor Administrator"        
                                                                                
   /*                                                                  */       
   /* Establish the environment (C1Deflts should be loaded now)        */       
   /* Do this after the first call to Endevor so that any exits        */       
   /* can process and correctly load the Defatuls table.               */       
   /*  - This will tell us the Customer Name and SiteID                */       
   /*  - But we could also use it to find other tables or to explore/  */       
   /*    expand the life-cycle map                                     */       
   /*                                                                  */       
   if DFCUNAME = "CUNAME unknown, Check ENUXSITE", /* If Enuxsite did  */       
    | DFCUNAME = '' then do  /* not work and we haven't loaded tab yet */       
      ADDRESS LINKMVS 'LOADTABL' 'C1DEFLTS' ; /* try again now         */       
      select                                                                    
         when RC = 0 then do                  /* all OK - save values  */       
            C1DEFLTS_ADDR = C2X(TBLADDR)                                        
            DFCUNAME = strip(STORAGE(C1DEFLTS_ADDR,50))                         
            POINTER_ADDR = D2X(X2D(C1DEFLTS_ADDR) + X2D(092))                   
            DFSITEID = STORAGE(POINTER_ADDR,01)                                 
            call Find_Env_map      /* carry on and get rest of env map */       
         end                                                                    
         when RC = -3 then do                                                   
            say "LOADTABL routine not found, please contact your Endevor Admin" 
            DFCUNAME = "CUNAME unknown, Check LOADTABL"                         
         end                                                                    
         when RC = -2054 then do                                                
            say "Defaults table(C1DEFLTS) not found, please" ,                  
                "contact your Endevor Admin"                                    
            DFCUNAME = "CUNAME unknown, Check C1DEFLTS"                         
         end                                                                    
         otherwise do                                                           
            Say "Unexpected RC:" RC,                                            
            "from LOADTABL routine, Please contact your Endevor Administrator"  
            DFCUNAME = "CUNAME unknown, Check LOADTABL/C1DEFLTS"                
         end                                                                    
      end                                                                       
      SA= "CUNAME:" DFCUNAME "SITEID:" DFSITEID                                 
   end                                                                          
                                                                                
   RETURN;                                                                      
                                                                                
                                                                                
Find_Env_Map: /* Discover Environment Map.                             */       
 ENV_NAME.                = "" /* initialize step variables to hold map */      
 NEXT_ENV.                = ""                                                  
 NEXT_STG.                = ""                                                  
 C1STAGE1.                = ""                                                  
 C1STAGE2.                = ""                                                  
 C1STGID1.                = ""                                                  
 C1STGID2.                = ""                                                  
 STAGE_1_ENTRY_INDICATOR. = ""                                                  
 STAGE_2_ENTRY_INDICATOR. = ""                                                  
 REF.                     = "" /* per env holds index to array */               
 ENT.                     = "" /*  .   .    .   entry stageid  */               
 LKS.                     = "" /*  .   .    .   count of back links */          
 FRM.                     = "" /*  .  link per env holds back ptr */            
                                                                                
 /* use address from LoadTabl to walk the Environment map */                    
 POINTER_ADDR = D2X(    X2D(C1DEFLTS_ADDR) + X2D(055)      )                    
 NUMBER_OF_ENVIRONMENTS = C2D(STORAGE(POINTER_ADDR,01));                        
/*                                                                   */         
 POINTER_ADDR = D2X(    X2D(C1DEFLTS_ADDR) + X2D(400)      )                    
/*                                                                   */         
 DO CNT = 1 TO NUMBER_OF_ENVIRONMENTS                                           
    ENV_NAME_ADDR = D2X(    X2D(POINTER_ADDR) + X2D(074)      )                 
    NEXT_ENV_ADDR = D2X(    X2D(POINTER_ADDR) + X2D(07C)      )                 
    NEXT_STG_ADDR = D2X(    X2D(POINTER_ADDR) + X2D(073)      )                 
    C1STAGE1_ADDR = POINTER_ADDR                                                
    C1STAGE2_ADDR = D2X(    X2D(POINTER_ADDR) + X2D(08A)      )                 
    C1STGID1_ADDR = D2X(    X2D(POINTER_ADDR) + X2D(0BE)      )                 
    C1STGID2_ADDR = D2X(    X2D(POINTER_ADDR) + X2D(0D3)      )                 
    STAGE_1_ENTRY_ADDR = D2X( X2D(POINTER_ADDR)+ X2D(0F0))                      
    STAGE_2_ENTRY_ADDR = D2X( X2D(POINTER_ADDR)+ X2D(0F1))                      
    ENV_NAME.CNT = STRIP(STORAGE(ENV_NAME_ADDR,08));                            
    NEXT_ENV.CNT = STRIP(STORAGE(NEXT_ENV_ADDR,08)) ;                           
    NEXT_STG.CNT = STRIP(STORAGE(NEXT_STG_ADDR,01)) ;                           
    C1STAGE1.CNT = STRIP(STORAGE(POINTER_ADDR,08)) ;                            
    C1STAGE2.CNT = STRIP(STORAGE(C1STAGE2_ADDR,08)) ;                           
    C1STGID1.CNT = STRIP(STORAGE(C1STGID1_ADDR,01)) ;                           
    C1STGID2.CNT = STRIP(STORAGE(C1STGID2_ADDR,01)) ;                           
    STAGE_1_ENTRY_INDICATOR.CNT  = STORAGE(STAGE_1_ENTRY_ADDR,01) ;             
    STAGE_2_ENTRY_INDICATOR.CNT  = STORAGE(STAGE_2_ENTRY_ADDR,01) ;             
    THISENV = ENV_NAME.CNT    /* point to this env */                           
    NEXTENV = NEXT_ENV.CNT    /* point to next env */                           
    REF.THISENV = CNT ;       /* save index so I can look up by environ  */     
    If STAGE_1_ENTRY_INDICATOR.CNT = "Y" then /* first stage entry?      */     
       ENT.THISENV = C1STGID1.CNT             /* Set entry stage to stg1 */     
    else                                      /* otherwise               */     
       ENT.THISENV = C1STGID2.CNT             /* Set entry stage to stg2 */     
    POINTER_ADDR = D2X(X2D(POINTER_ADDR) + X2D(100)) /* Point next entry */     
    IF NEXTENV \= "" Then DO                  /* if not the top of map   */     
       if LKS.NEXTENV = "" then               /* no prior links          */     
          LKS.NEXTENV = 1                     /* we have first link      */     
       else                                                                     
          LKS.NEXTENV = LKS.NEXTENV + 1       /* else, a new link        */     
       FRM.LKS.NEXTENV = THISENV              /* save this link          */     
    end                                                                         
 END  /* DO CNT = 1 TO NUMBER_OF_ENVIRONMENTS */                                
                                                                                
 ENV_NAME.0 = NUMBER_OF_ENVIRONMENTS          /* Save Num of Environments*/     
                                                                                
 /* show what we got */                                                         
 DO CNT = 1 TO ENV_NAME.0                                                       
    THISENV = ENV_NAME.CNT                                                      
    sa= "map data:",                                                            
    ENV_NAME.CNT,                                                               
    C1STAGE1.CNT || "/" ||,                                                     
    C1STGID1.CNT || "(" ||,                                                     
    STAGE_1_ENTRY_INDICATOR.CNT || ")" ,                                        
    C1STAGE2.CNT || "/" ||,                                                     
    C1STGID2.CNT || "(" ||,                                                     
    STAGE_2_ENTRY_INDICATOR.CNT || ")" ,                                        
    "===>" ,                                                                    
    NEXT_ENV.CNT || "/" ||,                                                     
    NEXT_STG.CNT ,                                                              
    "Entry Stage" ENT.THISENV                                                   
 END  /* DO CNT = 1 TO NUMBER_OF_ENVIRONMENTS */                                
                                                                                
RETURN;                                                                         
                                                                                
Load_ISPF_Table_For_Display:                                                    
  Unique_Ele# = 0 /* count elements listed */                                   
                                                                                
  SA= "Load_ISPF_Table_For_Display: "                                           
  "EXECIO * DISKR "LONGDDPR"CSVD (STEM API. finis"                              
  /* "FREE F("LONGDDPR"CSVD)" */                                                
  "FREE F(BSTIPT01)"                                                            
  IF API.0 < 2 THEN RETURN;                                                     
                                                                                
  varbs= API.1;                                                                 
                                                                                
  varbs = translate(varbs,"_"," ") ;                                            
  varbs = translate(varbs," ",'º"') ;                                           
  varbs = translate(varbs,"@","/") ;                                            
  varbs = translate(varbs,"@",")") ;                                            
  varbs = translate(varbs,"@","(") ;                                            
                                                                                
  Call CREATE_TABLE;                                                            
  Do rec# = 2 to API.0                                                          
     valus= API.rec# ;                                                          
     call SplitAtDelim   /* split the data at delimiters */                     
     Do # = 1 to Words(varbs)                                                   
     /*                                                                         
        if index(CSVWORD.#,"'") > 0 then /* any apost? */                       
          tmp = Word(varbs,#) '= "'translate(CSVWORD.#,'`','"')'"'              
        else                                                                    
          tmp = Word(varbs,#) "= '"translate(CSVWORD.#,"`","'")"'"              
        SA= # tmp ;                                                             
        interpret tmp; /* Note interpret has problems with very long names */   
                       /* Use Value instead - EOC*/                             
     */                                                                         
        if index(CSVWORD.#,"'") > 0 then /* any apost? */                       
          NewVal = translate(CSVWORD.#,'`','"')                                 
        else                                                                    
          NewVal = translate(CSVWORD.#,"`","'")                                 
        SA= # NewVal                                                            
        doit = value(Word(varbs,#),NewVal)                                      
     End; /* Do # = 1 to Words(varbs) */                                        
                                                                                
     /* Check for multiple headers  */                                          
     If FULL_ELM_NAME == 'FULL ELM NAME' then do                                
        ADDRESS ISPEXEC "SETMSG MSG(LONG018I) COND" /* multi pattern */         
        iterate                         /* go get next record */                
     end                                                                        
     /* First set the standard Q/E fields */                                    
     /* "STG NAME"º"STG ID"º"STG #"º"STG SEQ #" */                              
     EEVETKEL = FULL_ELM_NAME     /* Element Name     */                        
     EEVETKEN = ENV_NAME          /* Environment name */                        
     EEVETKSY = SYS_NAME          /* System Name      */                        
     EEVETKSB = SBS_NAME          /* SubSystem Name   */                        
     EEVETKTY = TYPE_NAME         /* Type Name        */                        
     EEVETKST = STG_#             /* Stage Number     */                        
     EEVETKSI = STG_ID            /* Stage ID         */                        
     EEVETKSN = STG_NAME          /* Stage Name       */                        
     EEVETDSL = STG_SEQ_#         /* Stage_Seq_number - up the map */           
     EEVETDVL = right(ELM_VV,2,'0') || right(ELM_LL,2,'0') ;                    
     if NOSOURCE == 'N' THEN EEVETNS = ' ' ; ELSE EEVETNS = NOSOURCE            
     EEVETPGR = PROC_GRP_NAME                                                   
     EEVETUID = ELM_LAST_LL_USRID       /* Q/E shows last element source */     
     EEVETPRC = right(PROC_RC,4,'0')    /* Thought these were four byte fields*/
     EEVETNRC = right(ENDEVOR_RC,4,'0') /* actually 5, consider just(right)   */
     EEVETSO  = SIGNOUT_ID                                                      
     EEVETCCI = LAST_ACT_CCID                                                   
     /* The following fields are not currently supported by QE */               
     LNLADATE = LAST_ACT_DATE                                                   
     LNLATIME = LAST_ACT_TIME                                                   
     LNUPDATE = UPDT_DATE                                                       
     LNUPTIME = UPDT_TIME                                                       
     LNMVDATE = MOVE_DATE                                                       
     LNMVTIME = MOVE_TIME                                                       
     LNGEDATE = GENERATE_DATE                                                   
     LNGETIME = GENERATE_TIME                                                   
     LNLACOMM = LAST_ACT_COMMENT                                                
     LNLASact = LAST_ACT                                                        
     LNElmNam = ELM_NAME                                                        
     LNDESCRP = BASE_COMMENT                                                    
     LNDPKGLK = PKG_ID                 /* current (locked) package id */        
     LNLKDATE = LOCKED_DATE                                                     
     LNLKTIME = LOCKED_TIME                                                     
     IF LNLKDATE /= '' then LNLOCKED = 'Y'; else LNLOCKED = 'N'                 
     LNDPKGIS = PKG_ID_@S@                                                      
     LNDPKGIO = PKG_ID_@O@                                                      
     LNDPKGDO = PKG_EXEC_DATE_@O@                                               
     LNDPKGBO = BACKED_OUT_@O@                                                  
     LNDGCCID = GENERATE_CCID          /* support for Alter Actions */          
     LNDRCCID = RETR_CCID              /* support for Alter Actions */          
     USERDATA = USER_DATA                                                       
     EEVETDMS = ' '                    /* message field should start as blanks*/
     LNCSVSEQ = RIGHT((rec# * 10),8,'0') /* generate a seq number based on csv*/
                                                                                
     /* because list element doesn't support the list where userid clause       
        I will do basic filtering here... (not placeholder filter yet)          
        */                                                                      
     IF EEVALF   == "Y" ,       /* do we have search filters? */                
      & EEVWUID  /= '' then do       /* UserID was specified? */                
         Wildcard = index(EEVWUID,'*')   /* with wildcard?    */                
         If Wildcard > 0 then                                                   
            if left(EEVWUID,Wildcard-1) == left(EEVETSO,Wildcard-1),            
             | left(EEVWUID,Wildcard-1) == left(EEVETUID,Wildcard-1),           
               then NOP /* we want this one */                                  
            else iterate /* skip it - doesn't match our filter */               
         else                                                                   
            IF EEVWUID == EEVETSO,                                              
             | EEVWUID == EEVETUID then nop /* match */                         
            else iterate                                                        
     end                                                                        
                                                                                
     /* Add a filter for Backout status                                         
        */                                                                      
     IF EEVALF   == "Y" ,       /* do we have search filters? */                
      & LNFILBK  == "Y" ,             /* only backouts on? */                   
       & LNDPKGBO /== "Y" then iterate  /* not backed out, skip it */           
                                                                                
     /* Add a filter for Locked status                                          
        */                                                                      
     IF EEVALF   == "Y" ,       /* do we have search filters? */                
      & LNFILLK  == "Y" ,             /* only Locked on? */                     
       & LNLOCKED /== "Y" then iterate  /* not Locked, skip it */               
                                                                                
     IF EEVALF   == "Y" ,       /* do we have search filters? */                
      & LNFILST  /= ""   then         /* and specific stages in mind */         
        if wordpos(EEVETKSI,TRANSLATE(LNFILST,' ',',')) = 0 then iterate        
                                                                                
     ADDRESS ISPEXEC "TBADD "LONGTABL" ORDER ";                                 
     TBADDRC = rc                                                               
     if TBADDRC = 0 then                 /* Everythign Fine      */             
        Unique_Ele# = Unique_Ele# + 1    /* count it             */             
     if TBADDRC = 8 then                 /* key already exists ? */             
        ADDRESS ISPEXEC "SETMSG MSG(LONG018W) COND" /* duplicate */             
                                                                                
  End; /* Do rec# = 1 to API.0 */                                               
                                                                                
  RETURN ;                                                                      
                                                                                
SplitAtDelim:                                                                   
  CSVWORDS. = ''                                                                
  CSVDATA = valus                                                               
  do i = 1 to Words(varbs)                                                      
    j = index(CSVDATA,'º')                                                      
    if J > 3 then do                       /* found next delimiter */           
      CSVWORD.I = substr(csvdata,2,(j-3))                                       
      CSVDATA = substr(csvdata,(j+1))                                           
    end                                                                         
    else                                                                        
      CSVWORD.I = ''                                                            
  end                                                                           
                                                                                
  return ;                                                                      
                                                                                
CREATE_TABLE:                                                                   
  SA= 'CREATE_TABLE               ' ;                                           
  ADDRESS ISPEXEC    "TBSTATS" LONGTABL "STATUS2(Stat2RC)"                      
  If Stat2RC = 4 then            /* table was open already?             */      
     ADDRESS ISPEXEC "TBCLOSE" LONGTABL /* try closing and re-creating  */      
  SA= "CREATE_TABLE "LONGTABL                                                   
  ADDRESS ISPEXEC,                                                              
    "TBCREATE" LONGTABL "REPLACE SHARE" ,                                       
    "KEYS(EEVETKEL EEVETKEN EEVETKSY EEVETKSB" ,                                
         "EEVETKTY EEVETKSI EEVETKST EEVETKSN" ,                                
         "EEVETDSL)",                                                           
    "NAMES(LNLACOMM LNDESCRP LNELMNAM LNLASACT LNDPKGIS" ,                      
          "LNLADATE LNLATIME LNUPDATE LNUPTIME" ,                               
          "LNMVDATE LNMVTIME LNGEDATE LNGETIME" ,                               
          "LNDPKGIO LNDPKGDO LNDPKGBO EEVETDVL EEVETDMS EEVETNS" ,              
          "EEVETPGR EEVETUID EEVETCCI EEVETPRC EEVETNRC EEVETSO" ,              
          "LNDPKGLK LNLKDATE LNLKTIME LNLOCKED",                                
          "USERDATA LNCSVSEQ LNDGCCID LNDRCCID) NOWRITE " ;                     
  RETURN;                                                                       
                                                                                
Display_Table:                                                                  
  SA= 'Display_Table:             ' ;                                           
                                                                                
   VARC1LR = PASSTHRU /* enable Left/Right commands */                          
   CURRTABL = LONGTABL /* current table */                                      
                                                                                
   /* check for a unique entry and valid command... */                          
   if Unique_Ele# = 1 &,           /* if we found a single element... */        
    thiscmd /== '' then do         /* and user entered a request */             
      sels = thiscmd               /* pass command as if entered on row */      
      thiscmd = ''                 /* and don't do this again */                
      TBDISPL_RC = 0               /* set up variables as if tbdispl rc=0 */    
      THISTOPR = '00000001'        /* ...only one record right...*/             
      ZTDSELS  = 1                 /* ...Act like one row to process */         
   end                                                                          
   else do                                                                      
      ADDRESS ISPEXEC "TBSORT "LONGTABL,                                        
              "FIELDS(EEVETKEL,C,A LNCSVSEQ,C,A) ";                             
      SA= 'COMPLETED TBSORT  ' ;                                                
      ADDRESS ISPEXEC "TBTOP   "LONGTABL ;                                      
      /* Show a list of selected elements                     */                
      ADDRESS ISPEXEC "TBDISPL "LONGTABL" PANEL("LONGPANL")"                 ;  
      TBDISPL_RC = RC ;                                                         
      If TBDISPL_RC >= 8  then,                                                 
         DO                                                                     
         ADDRESS ISPEXEC "TBEND "LONGTABL ;                                     
         Return                                                                 
         end;                                                                   
      THISTOPR = ZTDTOP      /* save the top row so we can restore it */        
   end                                                                          
/* DO WHILE ZTDSELS > 0  */                                                     
   DO FOREVER                                                                   
      Selected_EEVETDVL = " " ;                                                 
      SA= 'ZTDSELS=' ZTDSELS                                                    
      SA= "PROCESSING ELEMENT " EEVETKEL C1ELTYPE                               
      Entry = SELS,                                                             
          EEVETKEL EEVETKTY EEVETKEN EEVETKSI,                                  
           EEVETKSY EEVETKSB ;                                                  
      SA= "Selected " Entry;                                                    
      IF left(EEVETDMS,4) == "*Del" ,                                           
       | left(EEVETDMS,4) == "*Mov" ,                                           
       | left(EEVETDMS,4) == "*Xfr" ,                                           
       | EEVETDMS == "*Not Found" then do                                       
         SELS = ''                                                              
         ADDRESS ISPEXEC "SETMSG MSG(LONG021E)"   /* Refresh required */        
      END                                                                       
      ADDRESS ISPEXEC "VGET (EEVCCID EEVCOMM EEVOOSGN)" /*incase they changed*/ 
      SELS = Strip(SELS)                                                        
      SRCTYP = ""                         /* Default to EBCDIC               */ 
      IF SELS = "B"  then Call Browse_Element_TSO;                              
      IF SELS = "BX" then Call Browse_Element_TSO;                              
      IF SELS = "E"  then,                                                      
         Do                                                                     
         Call Retrieve_Element_TSO                                              
         If FGAPIRC <= 8 Then       /* don't try to edit if retrieve failed */  
           Call Edit_Element_TSO;                                               
         End;                                                                   
      IF SELS = "EA"  then,                                                     
         Do                                                                     
         SRCTYP = "ASCII"                  /* Special treatment for ASCII */    
         Call Retrieve_Element_TSO                                              
         If FGAPIRC <= 8 Then       /* don't try to edit if retrieve failed */  
           Call Edit_Element_TSO;                                               
         End;                                                                   
      IF SELS = "EU"  then,                                                     
         Do                                                                     
         SRCTYP = "UTF8"                   /* Special treatment for UniCode */  
         Call Retrieve_Element_TSO                                              
         If FGAPIRC <= 8 Then       /* don't try to edit if retrieve failed */  
           Call Edit_Element_TSO;                                               
         End;                                                                   
      IF SELS = "H"  then Call Browse_Element_TSO;                              
      IF SELS = "C"  then Call Browse_Element_TSO;                              
      IF SELS = "S"  then Call Browse_Element_TSO;                              
            /*   which Calls Summary_Of_Levels */                               
      IF SELS = "M"  then Call Browse_Element_TSO;                              
      IF SELS = "LL" then Call Browse_Element_TSO;                              
      IF SELS = "V"  then Call View_Element_TSO;                                
      IF SELS = "VA" then do                                                    
         SRCTYP = "ASCII"                  /* Special treatment for ASCII */    
         Call View_Element_TSO;                                                 
         END                                                                    
      IF SELS = "VU" then do                                                    
         SRCTYP = "UTF8"                   /* Special treatment for UniCide */  
         Call View_Element_TSO;                                                 
         END                                                                    
      IF SELS = "G"  then Call Generate_Element_Batch;                          
      IF SELS = "O"  then Call Move_Element_Batch;                              
      IF SELS = "T"  then Call Transfer_Element_Batch;                          
      IF SELS = "#"  then Call Delete_Element_Batch;                            
      IF SELS = "SI"  then Call SignIn_Element_TSO;                             
      IF (left(SELS,1)) = "U"  then Call User_Routine;                          
      IF (left(SELS,1)) = "D"  then Call User_Routine; /* Diff like user! */    
      SELS = '' ;                                                               
      EEVETKEL  = word(Entry,2) ;                                               
      EEVETKTY  = word(Entry,3) ;                                               
      EEVETKEN  = word(Entry,4) ;                                               
      EEVETKSI  = word(Entry,5) ;                                               
      EEVETKSY  = word(Entry,6) ;                                               
      EEVETKSB  = word(Entry,7) ;                                               
      ;                                                                         
                                                                                
   if ZTDSELS > 0 then            /* if there were any rows to process */       
      ADDRESS ISPEXEC "TBPUT "LONGTABL   /* save the updates          */        
                                                                                
   /* now check to see if there are any further updates pending... */           
                                                                                
   if ZTDSELS > 1 then do /* if there are more pending rows to display? */      
     ADDRESS ISPEXEC "TBDISPL  "LONGTABL; /* get the next selected entry */     
     iterate                                                                    
     end                                                                        
   THISCMD = ZCMD; /* get any command */                                        
   ZCMD = "";                     /* reset the command */                       
   parse upper var THISCMD THISCMDW THISCMDP                                    
                                                                                
   select                                                                       
     when THISCMDW == ''              then NOP;                                 
     when THISCMDW == 'LEFT'          then call Toggle_Screen;                  
     when THISCMDW == 'RIGHT'         then call Toggle_Screen;                  
     when THISCMDW == 'LOCATE'        then call Locate_line;                    
     when THISCMDW == 'L'             then call Locate_line;                    
     when THISCMDW == 'SUB'           then call Process_req;                    
     when THISCMDW == 'REFRESH'       then do                                   
       REFRESHR = 'YES'               /* request refresh */                     
       leave                                                                    
     end                                                                        
     when THISCMDW == 'ESORT' then do /* PS Sort request */                     
       ADDRESS ISPEXEC "SELECT CMD("THISCMD")"                                  
     end                                                                        
   otherwise                                                                    
     do                                                                         
        ZCMD = THISCMD /* restore command so it can be corrected */             
        ADDRESS ISPEXEC "SETMSG MSG(LONG013E)" ; /* Cmd Not Recognized */       
     end                                                                        
   end                                                                          
   ADDRESS ISPEXEC "TBTOP   "LONGTABL ;                                         
   ADDRESS ISPEXEC "TBSKIP  "LONGTABL" NUMBER("THISTOPR")"                      
   ADDRESS ISPEXEC "TBDISPL "LONGTABL" PANEL("LONGPANL")"                       
   TBDISPL_RC = RC ;                                                            
   If TBDISPL_RC >= 8  then,                                                    
      leave                                                                     
   THISTOPR = ZTDTOP      /* save the top row so we can restore it */           
   END; /* DO untill user presses END or RETURN */                              
                                                                                
   ADDRESS ISPEXEC "TBEND "LONGTABL ;                                           
                                                                                
   return ;                                                                     
                                                                                
Process_req:                                                                    
   ADDRESS ISPEXEC "VGET (EMULTREQ EMULTGT EMULPRM EMULTYPP) PROFILE"           
   ENABAUTO = 'N'                            /* TurnOff auto submit         */  
   ADDRESS ISPEXEC "VPUT (ENABAUTO) Shared"  /* and save for LONGSUBJ       */  
   if EMULTREQ > 0 then do                                                      
      ADDRESS ISPEXEC "SETMSG MSG(LONG020W)" ; /* Pending reqs!*/               
      sa=SAVESCL(RESET)                      /* Clear SCL (just in case)    */  
      call Show_JobCard                      /* Last Chance to submit?      */  
      end                                                                       
   else                                                                         
      ADDRESS ISPEXEC "SETMSG MSG(LONG020E)" ; /* Nothing to Submit */          
   return ;                                                                     
                                                                                
Locate_line:                                                                    
   if EVLNUPYN == 'Y' then                /* should we uppercase locate arg? */ 
     parse upper var THISCMD . THISLOCV . /* get the locate value uppercased */ 
   else                                   /* otherwise....                   */ 
     parse       var THISCMD . THISLOCV . /* leave it as entered             */ 
   ADDRESS ISPEXEC "TBVCLEAR "LONGTABL ;  /* Clear vars     */                  
   EEVETKEL = THISLOCV||'*'                                                     
   ADDRESS ISPEXEC "TBTOP    "LONGTABL ;  /* look Forwards  */                  
   ADDRESS ISPEXEC "TBSCAN   "LONGTABL" ARGLIST(EEVETKEL) CONDLIST(GE)",        
                   "POSITION(LOCTCRP) NEXT"                                     
   TBSCANRC = RC                                                                
   if TBSCANRC = 0 THEN THISTOPR = LOCTCRP                                      
   else DO                                                                      
      ZCMD = THISCMD /* restore command so it can be corrected */               
      ADDRESS ISPEXEC "SETMSG MSG(LONG014W)" ; /* Line not found */             
   end                                                                          
   if left(EEVETKEL,length(THISLOCV)) > THISLOCV then    /* did we overshoot? */
      ADDRESS ISPEXEC "TBSKIP  "LONGTABL" NUMBER(-1) POSITION(THISTOPR)"        
                                                                                
   Return;                                                                      
                                                                                
Toggle_Screen:                                                                  
                                                                                
   IF LONGPANL == LONGLIST THEN LONGPANL = "LONGLIS2"                           
   ELSE LONGPANL = "LONGLIST"                                                   
                                                                                
   Return;                                                                      
                                                                                
Toggle_Sum_Screen:                                                              
                                                                                
   IF LONGPANS == SUMLEVLS THEN LONGPANS = "SUMLEVL2"                           
   ELSE LONGPANS = "SUMLEVLS"                                                   
                                                                                
   Return;                                                                      
                                                                                
Browse_Element_TSO:                                                             
                                                                                
   /* Note: the LRECL is overwritten to be four bytes less than the block       
      size when when the file is opened for output as a print file.             
      To keep the file small enough to allow VIEW mode for listing              
      specify a block size that is small enough to not waste lots of            
      space that the Editor needs to "preserve" and there by avoid the          
      "browse substituted" message.  Additionally - keeping the record          
      length less than 255, allows the Editor to enable extended hilighting     
      when viewing the file (less important with Endevor HiLite enabled         
      which does not have this limitation)  Ideally we'd specify a more         
      efficient block size to limit I/O but managing the block size is          
      more important right now.                                                 
      */                                                                        
   ADDRESS TSO,                                                                 
      'ALLOC F('LONGDDPR'BROWS) LRECL(255) BLKSIZE(259) SPACE(5,10) ',          
       'DSORG(PS)',                                                             
        'RECFM(V B) CYL NEW CATALOG REUSE ' ;                                   
                                                                                
   LNLASACT = 'Print'                                                           
   sa=SAVESCL(RESET)                    /* reset our cache of SCL lines */      
   LNLASTGT = EEVETKSI                  /* Target Stage for action */           
   LNLASTYP = EEVETKTY                  /* Target Type for action  */           
   LNLASPRM = N                         /* Promote Eligible Flag N */           
   sa=SAVESCL(" PRINT ELEMENT ")                                                
   sa=SAVESCL("'"EEVETKEL"'")                                                   
   IF LENGTH(Selected_EEVETDVL) = 4 THEN,                                       
      sa=SAVESCL("  VERSION" SUBSTR(Selected_EEVETDVL,1,2),                     
               "LEVEL " SUBSTR(Selected_EEVETDVL,3,2))                          
   ELSE,                                                                        
      sa=SAVESCL("*  Current Version Level")                                    
   sa=SAVESCL("  FROM ENVIRONMENT "EEVETKEN " SYSTEM "EEVETKSY)                 
   sa=SAVESCL("    SUBSYSTEM "EEVETKSB " TYPE "EEVETKTY " STAGE" EEVETKSI)      
   sa=SAVESCL("  TO DDNAME '"LONGDDPR"BROWS' ")                                 
   Options = " ";                                                               
   If SELS = "H"  then Options  = Options "HISTORY"                             
   If SELS = "C"  then Options  = Options "CHANGE"                              
   If SELS = "S"  then Options  = Options "SUMMARY"                             
   If SELS = "M"  then Options  = Options "MASTER"                              
   If SELS = "LL" then Options  = Options "LISTING"                             
   If SELS = "BX" then Options  = Options "COMPONENT BROWSE"                    
   If SELS = "B"  then EEVETDMS = "*Browsed"                                    
   If SELS = "H"  then EEVETDMS = "*History"                                    
   If SELS = "C"  then EEVETDMS = "*Changes"                                    
   If SELS = "S"  then EEVETDMS = "*Summary"                                    
   If SELS = "M"  then EEVETDMS = "*Master"                                     
   If SELS = "LL" then EEVETDMS = "*Listing"                                    
   If SELS = "BX" then EEVETDMS = "*CompBrw"                                    
   If SELS = "LL" then                                                          
     sa=SAVESCL("  OPTIONS" Options".")                                         
   Else                                                                         
     sa=SAVESCL("  OPTIONS NOCC" Options".")                                    
   my_parms = SAVESCL(GETALL)           /* retrieve the accumulated SCL */      
   CALL Execute_API_SCL ;                                                       
                                                                                
   If SELS = "S"  then                                                          
      DO                                                                        
      CURRTABL = LONGTABS /* Setup for Summary Table */                         
      "ISPEXEC CONTROL DISPLAY SAVE"                                            
      Call Summary_Of_Levels                                                    
      "ISPEXEC CONTROL DISPLAY RESTORE"                                         
      CURRTABL = LONGTABL /* Return to main table */                            
      Return ;                                                                  
      End ;  /* If SELS = "S" */                                                
                                                                                
                                                                                
   EEVEDTIT = EEVETKEL' From: 'EEVETKEN'/'EEVETKSI'/',                          
                  ||EEVETKSY'/'EEVETKSB'/'EEVETKTY                              
   ADDRESS ISPEXEC "LMINIT DATAID(DDBR) DDNAME("LONGDDPR"BROWS)"                
   VARC1LR = ''       /* Allow standard Left/Right  */                          
   if SELS = "LL" then  /* did user want a listing?  Hilite messages */         
      ADDRESS ISPEXEC "VIEW   DATAID(&DDBR) PANEL(ISREDDHI) ",                  
                      "MACRO(ENDVHISC)"                                         
   else                                                                         
      ADDRESS ISPEXEC "VIEW   DATAID(&DDBR) PANEL(ENDIE300)",                   
                      "Profile("EEVETKTY")"                                     
   VARC1LR = PASSTHRU /* enable Left/Right commands */                          
   ADDRESS ISPEXEC "LMFREE DATAID(&DDBR)"                                       
   /* "FREE F("LONGDDPR"BROWS)" */                                              
                                                                                
   return;                                                                      
                                                                                
Edit_Element_TSO:                                                               
                                                                                
   If SRCTYP == "ASCII",                     /* processing for ASCII files */   
    | SRCTYP == "UTF8"  then do              /* or Unicode           files */   
      AsciiFil = LongTempDir"/"EEVETKEL      /* set up full path name      */   
      address ispexec 'vput (AsciiFil)'      /* and save it                */   
      handle = "file(AsciiFil)"              /* set a handle for the file  */   
      end                                                                       
   Else                                                                         
      do                                                                        
      handle = "DATAID(&DDED)"               /* set a handle for DD'd file */   
      ADDRESS ISPEXEC "LMINIT   DATAID(DDED) DDNAME("LONGDDPR"SRCE)"            
      If RC > 0 then DO                                                         
         EEVETDMS = "*Error"                                                    
         Return;                                                                
         end                                                                    
      end                                                                       
                                                                                
   FORCESAV = 'NO'                          /* Reset autosave flag         */   
   ADDRESS ISPEXEC "VPUT (FORCESAV) Shared" /* ...and save it              */   
                                                                                
   /* do edit/save cycle until we have a good RC, Or user Cancels          */   
   do forever                                                                   
      /* Challange!  I don't know entry stage, and Element name could be long   
         if user specified multiple or wildcarded subsystem then we must        
         use the found subsystem when doing the add, otherwise the req subsys   
      */                                                                        
      if LNPSBS /= ' ',                                                         
       & index(LNPSBS,'*') < 1,                                                 
        & index(LNPSBS,'%') < 1,                                                
         & words(LNPSBS) = 1 then,                                              
           TGTSBSYS = LNPSBS           /* use fully qualified subsys */         
      else                                                                      
           TGTSBSYS = EEVETKSB         /* use subsys where found */             
      EEVEDTIT = EEVETKEL' To: 'C1ENVMNT'/' || , /* set title text */           
                     ENT.C1ENVMNT || '/' || EEVETKSY'/' ||,                     
                     TGTSBSYS || '/' || EEVETKTY                                
      /* save a copy of the current element variables so they can be used       
         by IMACRO and/or user for display etc. */                              
      address ispexec 'vput (EEVETKEL C1ENVMNT EEVETKSY EEVETKSB EEVETKTY' ,    
                      'EEVOOSGN EEVCCID EEVCOMM FORCESAV) shared'               
                                                                                
                                                                                
      address ispexec 'vput (LNLACOMM LNDESCRP LNELMNAM LNLASACT' ,             
                      'LNLADATE LNLATIME LNUPDATE LNUPTIME' ,                   
                      'LNMVDATE LNMVTIME LNGEDATE LNGETIME' ,                   
                      'LNDPKGIS LNDPKGIO LNDPKGDO LNDPKGBO EEVETDVL' ,          
                      'EEVETDMS EEVETNS  EEVETPGR EEVETUID EEVETCCI' ,          
                      'EEVETPRC EEVETNRC EEVETSO  USERDATA LNCSVSEQ' ,          
                      'LNDPKGLK LNLKDATE LNLKTIME LNLOCKED',                    
                      'EEVETKSI LNDGCCID LNDRCCID) shared' ;                    
      /* check if line cmds are in use */                                       
      address ispexec 'VGET (ZLMAC)'                                            
      if ZLMAC /= '' THEN CMDOPT = 'LINECMDS('ZLMAC')'                          
      else                CMDOPT = ''                                           
      VARC1LR = ''       /* Allow standard Left/Right  */                       
      /* check if Preserve Required */                                          
      address ispexec 'VGET (EEVOOPRE)'                                         
      if EEVOOPRE == 'Y' THEN PREOPT = 'PRESERVE'                               
      else                    PREOPT = ''                                       
      VARC1LR = ''       /* Allow standard Left/Right  */                       
      ADDRESS ISPEXEC "EDIT" handle "PANEL(ENDIE300)",                          
                      "Profile("EEVETKTY")",                                    
                      "MACRO(LONGIMAC)" CMDOPT PREOPT SRCTYP                    
      Edit_RC = RC;                                                             
      VARC1LR = PASSTHRU /* enable Left/Right commands */                       
      /* Finished Editing give up Enqueue so API can run...          */         
      ADDRESS LINKMVS 'CTLIENQ DName'      /* DeQueue Element        */         
                                                                                
      If Edit_RC > 0 then DO                                                    
         If SRCTYP == "ASCII" ,                                                 
          | SRCTYP == "UTF8" then do                                            
               call bpxwunix "rm -f '"LongTempDir"/"EEVETKEL"'"                 
               call bpxwunix "rmdir '"LongTempDir"'"                            
            end                                                                 
         Else                                                                   
            ADDRESS ISPEXEC "LMFREE" handle                                     
         EEVETDMS = "*Canceled"                                                 
         ADDRESS LINKMVS 'CTLIENQ XName'      /* DeQueue System      */         
         Return;                                                                
      end                                                                       
                                                                                
      LNLASACT = 'Edit'                                                         
      ADDRESS ISPEXEC "VGET (EEVCCID EEVCOMM EEVOOSGN)" /*incase they changed*/ 
      sa=SAVESCL(RESET)                 /* reset our cache of SCL lines */      
      LNLASTGT = ENT.C1ENVMNT           /* entry stage of environment */        
      LNLASPRM = N                      /* Promote Eligible Flag N */           
      LNLASTYP = EEVETKTY               /* Target Type for action  */           
      sa=SAVESCL(" ADD ELEMENT ")                                               
      sa=SAVESCL("'"EEVETKEL"'")                                                
      /* if user specified multiple or wildcarded subsystem then we must        
         use the found subsystem when doing the add. */                         
      if LNPSBS /= ' ',                                                         
       & index(LNPSBS,'*') < 1,                                                 
        & index(LNPSBS,'%') < 1,                                                
         & words(LNPSBS) = 1 then,                                              
           EEVETKSB = LNPSBS ;         /* use fully qualified subsys */         
      sa=SAVESCL("  TO ENVIRONMENT "C1ENVMNT " SYSTEM "EEVETKSY)                
      sa=SAVESCL("    SUBSYSTEM "EEVETKSB " TYPE "EEVETKTY)                     
      If SRCTYP = "ASCII" ,                                                     
       | SRCTYP = "UTF8" then do                                                
         sa=SAVESCL("  FROM PATH '"LongTempDir"/' HFSFILE ")                    
         sa=SAVESCL("'"EEVETKEL"'")                                             
         end                                                                    
      Else                                                                      
         sa=SAVESCL("  FROM DDNAME '"LONGDDPR"SRCE' ")                          
      Options = " " ;                                                           
      IF EEVOOSGN = "Y" then Options = "OVERRIDE SIGNOUT" ;                     
      IF EEVETSO  = '' then Options = "OVERRIDE SIGNOUT" ;                      
      IF EEVPRGRP /= "" then Options = options "PRO GRO =" EEVPRGRP             
      sa=SAVESCL("  OPTIONS CCID '"EEVCCID"'" Options)                          
      IF index(EEVCOMM,"'") > 0 then    /* if comment has an apost */           
        sa=SAVESCL('      COMMENT "'EEVCOMM'"')                                 
      else                                                                      
        sa=SAVESCL("      COMMENT '"EEVCOMM"'")                                 
      sa=SAVESCL("      BYPASS GENERATE PROCESSOR UPDATE IF PRESENT.")          
      my_parms = SAVESCL(GETALL)        /* retrieve the accumulated SCL       */
                                                                                
      CALL Execute_API_SCL ;                                                    
      If FGAPIRC <= 8 then leave        /* File has been saved, we can leave  */
                                                                                
    /* otherwise - something went wrong with add/update - tell user and re-try*/
      ADDRESS LINKMVS 'CTLIENQ EName'       /* Try to re-grab enque  */         
      If rc > 0 then                       /* Element in use BAD!   */          
      DO                                                                        
        ADDRESS ISPEXEC "SETMSG MSG(ENDE056E)"                                  
        EEVETDMS = "*InUse"                                                     
        FGAPIRC = 16                                                            
        leave                                                                   
      END                                                                       
      FORCESAV = 'YES'                  /* set flag to reset autosave mode    */
      ADDRESS ISPEXEC "VPUT (FORCESAV) Shared" /* ...and save it              */
      ADDRESS ISPEXEC "SETMSG MSG(LONG027E)"  /* warn user update failed      */
   end                                  /* Loop back and re-edit the file     */
                                                                                
/* All Done - give up any enqueues                                            */
      ADDRESS LINKMVS 'CTLIENQ DName'      /* DeQueue Element        */         
      ADDRESS LINKMVS 'CTLIENQ XName'      /* DeQueue System         */         
/* When I'm sure I'm finished with the temp edit file, free it and the handle */
   If SRCTYP == "ASCII" ,                                                       
    | SRCTYP == "UTF8" then do                                                  
         call bpxwunix "rm -f '"LongTempDir"/"EEVETKEL"'"                       
         call bpxwunix "rmdir '"LongTempDir"'"                                  
      end                                                                       
   Else do                                                                      
         ADDRESS ISPEXEC "LMFREE" handle                                        
         "FREE F("LONGDDPR"SRCE)"                                               
      end                                                                       
/* CHeck if we are firsteating a new element (yes return to Entry panel) */     
   If THISCMD = 'CR' then return                                                
/* check C1MSGS for Add to Env/Sys/Stage... */                                  
   "EXECIO * DISKR C1MSGS1 (STEM MS1. finis"                                    
                                                                                
   THISTDVL = EEVETDVL           /* Current VersionLevel */                     
   NOELECHG = 'NO'               /* Reset no change flag */                     
                                                                                
   Do rec# = 1 to MS1.0                                                         
    /* Copyright (C) xxxx-yyyy CA. All Rights Reserved...29MAY14 17:28:28 PAG */
      IF INDEX(MS1.rec#,'Copyright (C)') > 0 then do;                           
        PARSE VAR MS1.rec# . 'Reserved. '   THISDATE ,                          
                                            THISTIME ,                          
                             'PAGE'         THISPAGE .                          
        THISDATE = STRIP(THISDATE)                                              
        THISTIME = STRIP(THISTIME)                                              
        THISPAGE = STRIP(THISPAGE)                                              
      END                                                                       
    /* SMGR122W  NO ELEMENT|COMPONENT SOURCE CHANGES DETECTED  */               
      IF INDEX(MS1.rec#,'SMGR122W  NO ELEMENT SOURCE CHANGES') > 0 then do;     
        NOELECHG = 'YES'                                                        
      END                                                                       
    /* C1G0204I  TO ENV...: env SYSTEM: sys SUB...: sub TYPE: typ STAGE ID: e */
      IF INDEX(MS1.rec#,'C1G0204I') > 0 then do;                                
        PARSE VAR MS1.rec# . 'ENVIRONMENT:' THISTKEN ,                          
                             'SYSTEM:'      THISTKSY ,                          
                             'SUBSYSTEM:'   THISTKSB ,                          
                             'TYPE:'        THISTKTY ,                          
                             'STAGE ID:'    THISTKSI                            
        THISTKEN = STRIP(THISTKEN)                                              
        THISTKSY = STRIP(THISTKSY)                                              
        THISTKSB = STRIP(THISTKSB)                                              
        THISTKTY = STRIP(THISTKTY)                                              
        THISTKSI = STRIP(THISTKSI)                                              
      END                                                                       
    /* SMGR136I  ELEMENT VVLL 0101 CHANGES INCLUDE 1 LINES INSERTED AND ...*/   
      IF INDEX(MS1.rec#,'SMGR136I') > 0 then do;                                
        PARSE VAR MS1.rec# . 'VVLL' THISTDVL ,                                  
                             'CHANGES INCLUDE' THIS#INS ,                       
                          'LINES INSERTED AND' THIS#DEL .                       
        THISTDVL = STRIP(THISTDVL)                                              
        THIS#INS = STRIP(THIS#INS)                                              
        THIS#DEL = STRIP(THIS#DEL)                                              
      END                                                                       
   END                                                                          
   IF LENGTH(Selected_EEVETDVL) = 4 THEN,  /* working with a specific level */  
     do                                                                         
       if NOELECHG == 'YES' then           /* no changes? */                    
          SUMMSG = "*NoChanges"                                                 
       else                                                                     
       do                                                                       
          SUMMSG = "*Fetched"                                                   
          ADDRESS ISPEXEC "TBPUT   "LONGTABS         ;                          
          if THISTDVL <> Selected_EEVETDVL THEN do                              
            ADDRESS ISPEXEC "TBBOTTOM "LONGTABS" NOREAD"        ;               
            USER    = USERID()                                                  
            SUMDATE = THISDATE                                                  
            SUMTIME = THISTIME                                                  
            STMTS   = '?'                  /* would need to calc */             
            INSERTS = THIS#INS                                                  
            DELETES = THIS#DEL                                                  
            SUMSYNC = '?'                                                       
            EEVETDVL = THISTDVL                                                 
            SUMMSG  = "*Edited"                                                 
            ADDRESS ISPEXEC "TBADD   "LONGTABS" ORDER"        ;                 
          end                                                                   
       end                                                                      
     end                                                                        
   else                                                                         
     do                                                                         
      /* Is this a new row? */                                                  
      IF THISTKEN||THISTKSY||THISTKSB||THISTKTY||THISTKSI == ,                  
         EEVETKEN||EEVETKSY||EEVETKSB||EEVETKTY||EEVETKSI Then                  
         do                                                                     
           EEVETDVL = THISTDVL      /* update the new ver/lvl */                
           EEVETCCI = EEVCCID                                                   
           LNLADATE = '20' || date(o) /* return YYYY/mm/DD */                   
           LNLATIME = THISTIME                                                  
           EEVETUID = USERID()                                                  
           EEVETSO  = USERID()                                                  
         end                                                                    
      Else                 /* otherwise it's been added at the entry stage */   
         do                                                                     
           EEVETSO  = USERID()                                                  
           EEVETDMS = "*Fetched"                                                
           ADDRESS ISPEXEC "TBPUT  "LONGTABL ;                                  
           ADDRESS ISPEXEC "TBSKIP "LONGTABL" NUMBER(-1) NOREAD";               
           EEVETKEN = THISTKEN                                                  
           EEVETKSY = THISTKSY                                                  
           EEVETKSB = THISTKSB                                                  
           EEVETKTY = THISTKTY                                                  
           EEVETKSI = THISTKSI                                                  
           EEVETDVL = THISTDVL      /* update the new ver/lvl */                
           EEVETCCI = EEVCCID                                                   
           LNLADATE = '20' || date(o) /* return YYYY/mm/DD */                   
           LNLATIME = THISTIME                                                  
           EEVETUID = USERID()                                                  
           EEVETSO  = USERID()                                                  
           LNCSVSEQ = LEFT(lncsvseq,7) || '5' /* insert before parent */        
           EEVETDMS = "*Saved/Sub"                                              
           ADDRESS ISPEXEC "TBADD "LONGTABL" ORDER ";                           
         end                                                                    
                                                                                
      If FGAPIRC < 12  THEN DO                                                  
         EEVETDMS = "*Saved/Sub"                                                
         CALL Generate_Element_Batch ;                                          
         if JobCard_RC > 0 then        /* did submit go ahead? */               
           EEVETDMS = "*Save/NoSub"                                             
      end                                                                       
      Else                                                                      
         EEVETDMS = "*Error"                                                    
     end                                                                        
                                                                                
   return;                                                                      
                                                                                
View_Element_TSO:                                                               
                                                                                
   EEVETDMS = "*Viewed"                                                         
   Call Retrieve_Element_TSO ;                                                  
   If FGAPIRC >  8 Then do    /* don't try to view if retrieve failed */        
     EEVETDMS = "*Error:"FGAPIRC                                                
     return                                                                     
   end                                                                          
                                                                                
   If SRCTYP == "ASCII" ,                    /* processing for ASCII files */   
    | SRCTYP == "UTF8" then do               /* processing for UTF8 files */    
      AsciiFil = LongTempDir"/"EEVETKEL      /* set up full path name      */   
      address ispexec 'vput (AsciiFil)'      /* and save it                */   
      handle = "file(AsciiFil)"              /* set a handle for the file  */   
      end                                                                       
   Else                                                                         
      do                                                                        
      handle = "DATAID(&DDVI)"               /* set a handle for DD'd file */   
      ADDRESS ISPEXEC "LMINIT   DATAID(DDVI) DDNAME("LONGDDPR"SRCE)"            
      If RC > 0 then DO                                                         
         EEVETDMS = "*Error"                                                    
         Return;                                                                
         end                                                                    
      end                                                                       
                                                                                
   LNLASACT = 'Retrieve'                                                        
   EEVEDTIT = EEVETKEL' From: 'EEVETKEN'/'EEVETKSI'/',                          
                  ||EEVETKSY'/'EEVETKSB'/'EEVETKTY                              
   /* check if line cmds are in use */                                          
   address ispexec 'VGET (ZLMAC)'                                               
   if ZLMAC /= '' THEN CMDOPT = 'LINECMDS('ZLMAC')'                             
   else CMDOPT = ''                                                             
   VARC1LR = ''       /* Allow standard Left/Right  */                          
   ADDRESS ISPEXEC "VIEW" handle "PANEL(ENDIE300)",                             
                   "Profile("EEVETKTY")",                                       
                   "MACRO(LONGIMAC)" CMDOPT SRCTYP                              
                                                                                
   VARC1LR = PASSTHRU /* enable Left/Right commands */                          
                                                                                
/* When I'm sure I'm finished with the temp edit file, free it and the handle */
   If SRCTYP == "ASCII" ,                                                       
    | SRCTYP == "UTF8" then do                                                  
         call bpxwunix "rm -f '"LongTempDir"/"EEVETKEL"'"                       
         call bpxwunix "rmdir '"LongTempDir"'"                                  
      end                                                                       
   Else do                                                                      
         ADDRESS ISPEXEC "LMFREE" handle                                        
         "FREE F("LONGDDPR"SRCE)"                                               
      end                                                                       
                                                                                
   return ;                                                                     
                                                                                
Generate_Element_Batch :                                                        
                                                                                
   LNLASACT = 'Generate'                                                        
   sa=SAVESCL(RESET)                    /* reset our cache of SCL lines */      
   LNLASTGT = EEVETKSI                  /* Target Stage for action */           
   LNLASTYP = EEVETKTY                  /* Target Type for action  */           
   LNLASPRM = N                         /* Promote Eligible Flag N */           
   sa=SAVESCL(" GENERATE ELEMENT ")                                             
   sa=SAVESCL("'"EEVETKEL"'")                                                   
   sa=SAVESCL("  FROM ENVIRONMENT "EEVETKEN " SYSTEM "EEVETKSY)                 
   sa=SAVESCL("    SUBSYSTEM "EEVETKSB " TYPE "EEVETKTY)                        
   sa=SAVESCL("      STAGE '"EEVETKSI"'")                                       
   Options = " " ;                                                              
   IF EEVOOSGN = "Y" then Options = "OVER SIGNO" ;                              
   IF EEVPRGRP /= "" then Options = options "PRO GRO =" EEVPRGRP                
   sa=SAVESCL("  OPTIONS CCID '"EEVCCID"'" Options)                             
   IF index(EEVCOMM,"'") > 0 then /* if comment has an apost */                 
     sa=SAVESCL('          COMMENT "'EEVCOMM'".')                               
   else                                                                         
     sa=SAVESCL("          COMMENT '"EEVCOMM"'.")                               
 /*sa=SAVESCL("  ")*/                                                           
                                                                                
   "ISPEXEC CONTROL DISPLAY SAVE"                                               
   EEVETDMS = "*Gen/Sub"                                                        
   Call Show_JobCard;                                                           
   "ISPEXEC CONTROL DISPLAY RESTORE"                                            
                                                                                
   return ;                                                                     
                                                                                
Move_Element_Batch :                                                            
   LNLASACT = 'Move'                                                            
   sa=SAVESCL(RESET)                    /* reset our cache of SCL lines */      
   THIS = REF.EEVETKEN                  /* get the index for this env */        
   if EEVETKSI = C1STGID2.THIS THEN     /* Are we at stage 2 already? */        
      LNLASTGT = NEXT_STG.THIS          /* Target Stage is next up map */       
   else                                 /* otherwise that's where we going */   
      LNLASTGT = C1STGID2.THIS          /* Target Stage for action */           
   if LNLASTGT = "" THEN                /* If there are no more stages*/        
      LNLASTGT = '#'                    /* Indicate we're at the top of map*/   
   LNLASPRM = Y                         /* Promote Eligible Flag Y */           
   LNLASTYP = EEVETKTY                  /* Target Type for action  */           
   sa=SAVESCL(" MOVE ELEMENT ")                                                 
   sa=SAVESCL("'"EEVETKEL"'")                                                   
   sa=SAVESCL("  FROM ENVIRONMENT "EEVETKEN " SYSTEM "EEVETKSY)                 
   sa=SAVESCL("    SUBSYSTEM "EEVETKSB " TYPE "EEVETKTY)                        
   sa=SAVESCL("      STAGE '"EEVETKSI"'")                                       
   sa=SAVESCL("  OPTIONS CCID '"EEVCCID"'")                                     
   IF index(EEVCOMM,"'") > 0 then /* if comment has an apost */                 
     sa=SAVESCL('          COMMENT "'EEVCOMM'".')                               
   else                                                                         
     sa=SAVESCL("          COMMENT '"EEVCOMM"'.")                               
 /*sa=SAVESCL("  ")*/                                                           
                                                                                
   "ISPEXEC CONTROL DISPLAY SAVE"                                               
   EEVETDMS = "*Move/Sub"                                                       
   Call Show_JobCard;                                                           
   "ISPEXEC CONTROL DISPLAY RESTORE"                                            
                                                                                
   return ;                                                                     
                                                                                
Delete_Element_Batch :                                                          
   LNLASACT = 'Delete'                                                          
   sa=SAVESCL(RESET)                    /* reset our cache of SCL lines */      
   LNLASTGT = EEVETKSI                  /* Target Stage for action */           
   LNLASTYP = EEVETKTY                  /* Target Type for action  */           
   LNLASPRM = N                         /* Promote Eligible Flag N */           
   sa=SAVESCL(" DELETE ELEMENT ")                                               
   sa=SAVESCL("'"EEVETKEL"'")                                                   
   sa=SAVESCL("  FROM ENVIRONMENT "EEVETKEN " SYSTEM "EEVETKSY)                 
   sa=SAVESCL("    SUBSYSTEM "EEVETKSB " TYPE "EEVETKTY)                        
   sa=SAVESCL("      STAGE '"EEVETKSI"'")                                       
   Options = " " ;                                                              
   IF EEVOOSGN = "Y" then Options = "OVERRIDE SIGNOUT" ;                        
   sa=SAVESCL("  OPTIONS CCID '"EEVCCID"'" Options)                             
   IF index(EEVCOMM,"'") > 0 then /* if comment has an apost */                 
     sa=SAVESCL('          COMMENT "'EEVCOMM'".')                               
   else                                                                         
     sa=SAVESCL("          COMMENT '"EEVCOMM"'.")                               
 /*sa=SAVESCL("  ")*/                                                           
                                                                                
   "ISPEXEC CONTROL DISPLAY SAVE"                                               
   EEVETDMS = "*Del/Sub"                                                        
   Call Show_JobCard;                                                           
   "ISPEXEC CONTROL DISPLAY RESTORE"                                            
                                                                                
   return ;                                                                     
                                                                                
Transfer_Element_Batch :                                                        
                                                                                
   "ISPEXEC CONTROL DISPLAY SAVE"                                               
   ENVIRON = EEVETKEN                                                           
   SYSTEM  = EEVETKSY                                                           
   SUBSYS  = EEVETKSB                                                           
   ELEMENT = EEVETKEL                                                           
   TYPE    = EEVETKTY                                                           
   STAGE   = EEVETKSI                                                           
   ADDRESS ISPEXEC "DISPLAY PANEL(C1SF900L) "                                   
   Panel_RC = RC;                                                               
   If Panel_RC > 0 then,                                                        
      Do                                                                        
        "ISPEXEC CONTROL DISPLAY RESTORE"                                       
        EEVETDMS = "*Canceled"                                                  
        Return;                                                                 
      End;                                                                      
                                                                                
   LNLASACT = 'Transfer'                                                        
   sa=SAVESCL(RESET)                    /* reset our cache of SCL lines */      
   LNLASTGT = VNBF9STG                  /* Target Stage for action */           
   IF VNBF9NOD \= "N",                  /* Are we deleting the source? */       
    & EEVETKSI \= VNBF9STG then         /* and the source / tgt not same? */    
      LNLASTGT = '@'                    /* Flag as multi-target */              
   LNLASTYP = VNBF9TYP                  /* Target Type for action  */           
   LNLASPRM = N                         /* Promote Eligible Flag N */           
   sa=SAVESCL(" TRANSFER ELEMENT ")                                             
   sa=SAVESCL("'"EEVETKEL"'")                                                   
   sa=SAVESCL("  FROM ENV "EEVETKEN" SYS "EEVETKSY,                             
                  "SUB "EEVETKSB" TYPE "EEVETKTY "STAGE '"EEVETKSI"'")          
   sa=SAVESCL("  TO   ENV "VNBF9ENV" SYS "VNBF9SYS,                             
                  "SUB "VNBF9SBS" TYPE "VNBF9TYP "STAGE '"VNBF9STG"'")          
   IF EEVETKEL /== ELEMENTT THEN do                                             
      sa=SAVESCL("     ELEMENT")                                                
      sa=SAVESCL("'"ELEMENTT"'")                                                
   End;                                                                         
   sa=SAVESCL("  OPTIONS CCID '"EEVCCID"'")                                     
   IF index(EEVCOMM,"'") > 0 then /* if comment has an apost */                 
     sa=SAVESCL('          COMMENT "'EEVCOMM'"')                                
   else                                                                         
     sa=SAVESCL("          COMMENT '"EEVCOMM"'")                                
   Options = " " ;                                                              
   IF C1SISOFR = "Y" then Options = "OVE SIGN" ;                                
   IF VNBF9HIS = "Y" then Options = Options "WITH HIST" ;                       
   IF VARSYNC  = "Y" then Options = Options "SYN" ;                             
   IF VARRTNSO = "Y" then Options = Options "RETA SIGNO" ;                      
   IF VNBF9NOG = "N" then Options = Options "BYP GEN PRO" ;                     
   IF VNBF9NOD = "N" then Options = Options "BYP ELE DEL" ;                     
   IF EVLNUIGN = "Y" then Options = Options "IGN" ;                             
   IF Options  = " " then Options = ".      * No options"                       
   sa=SAVESCL("       "Options".")                                              
 /*sa=SAVESCL("  ")*/                                                           
                                                                                
   EEVETDMS = "*Xfr/Sub"                                                        
   Call Show_JobCard;                                                           
   "ISPEXEC CONTROL DISPLAY RESTORE"                                            
   return ;                                                                     
                                                                                
Create_New_Element :                                                            
                                                                                
   EEVETKEL = QEPELML                                                           
   EEVETSO  = USERID()                                                          
   EEVETKEN = QEPEVNME                                                          
   C1ENVMNT = QEPEVNME                                                          
   EEVETKSY = QEPSYS                                                            
  C1SYSTEM  = QEPSYS                                                            
   EEVETKSB = LNPSBS                                                            
 C1SUBSYS   = LNPSBS                                                            
   EEVETKTY = QEPTYP                                                            
  C1ELTYPE  = QEPTYP                                                            
   EEVETKSI = '*'                                                               
   EEVETCCI = EEVCCID                                                           
                                                                                
      EleKey = 'E' || ,                    /*Endevor Element enq    */          
               DFSITEID ||,                /*Use SiteID from C1DFLTS*/          
               left(C1ENVMNT,08) ||,       /*Environment            */          
               left(EEVETKSY,08) ||,       /*System                 */          
               left(EEVETKSB,08) ||,       /*SubSystem              */          
               left(EEVETKEL,10) ||,       /*Ele(not encrypted yet) */          
               left(EEVETKTY,08)           /*Type                   */          
      SName = 'S' LEFT('S'||Substr(EleKey,2,17),44)/* Sys Enq Shrd  */          
      EName = 'E' EleKey                   /* Enq request Parm      */          
      DName = 'D' EleKey                   /* Deq request Parm      */          
      XName = 'D' Substr(SName,3)          /* System DeQueue (SHRD) */          
      ADDRESS LINKMVS 'CTLIENQ SName'      /* Try Enq for System    */          
      If rc > 0 then                       /* System being back'd up*/          
      DO                                                                        
        EEVSYTSY = EEVETKSY                /* system code for mesg  */          
        ADDRESS ISPEXEC "SETMSG MSG(ENDE057E)"                                  
        FGAPIRC = 16                                                            
        return                                                                  
      END                                                                       
      ADDRESS LINKMVS 'CTLIENQ EName'      /* Try Ele Enqueue Now   */          
      If rc > 0 then                       /* Element in use        */          
      DO                                                                        
        ADDRESS ISPEXEC "SETMSG MSG(ENDE056E)"                                  
        FGAPIRC = 16                                                            
        return                                                                  
      END                                                                       
   Call Get_Type_LRECL_and_BLKSIZE ;                                            
                                                                                
   ADDRESS TSO,                                                                 
   "ALLOC F("LONGDDPR"SRCE) LRECL("LRECL") BLKSIZE("BLKSIZE") SPACE(5,5) ",     
    "DSORG(PS)",                                                                
     "RECFM("RECFM") TRACKS NEW CATALOG REUSE " ;                               
                                                                                
  EEVETDMS = "*Created"                                                         
  SRCTYP = ""            /* Default to NO Special treatment for EBCDIC */       
  Call Edit_Element_TSO ;                                                       
                                                                                
  if EEVETDMS == "*Canceled" then                                               
    ADDRESS ISPEXEC "SETMSG MSG(ENDE046E)" ; /* Request cancelled */            
  else do                                                                       
    EEVELMNM = QEPELML;                                                         
    ADDRESS ISPEXEC "SETMSG MSG(ENDE030I)" ; /* Element Created */              
    LNLASACT = 'Add'                                                            
    CALL Generate_Element_Batch ;            /* All OK, now generate */         
  end                                                                           
                                                                                
  /* All Done - give up any enqueues                             */             
  ADDRESS LINKMVS 'CTLIENQ DName'      /* DeQueue Element        */             
  ADDRESS LINKMVS 'CTLIENQ XName'      /* DeQueue System         */             
                                                                                
  RETURN;                                                                       
                                                                                
Retrieve_Element_TSO :                                                          
                                                                                
   if left(SELS,1) == "E" &,               /* is this an Edit request?   */     
      EEVOOSGN /= "Y" &,                   /* and not override signout   */     
      EEVETSO  /= '' &,                    /* and not signed in          */     
      USERID() /= EEVETSO THEN,            /* and not signed out to user */     
      DO                                                                        
        ADDRESS ISPEXEC "SETMSG MSG(LONG010I)" ;                                
        FGAPIRC = 12                                                            
        return                                                                  
      END                                                                       
                                                                                
   if left(SELS,1) == "E" Then do          /* If Edit, check in use */          
      EleKey = 'E' || ,                    /*Endevor Element enq    */          
               DFSITEID ||,                /*Use SiteID from C1DFLTS*/          
               left(C1ENVMNT,08) ||,       /*Environment            */          
               left(EEVETKSY,08) ||,       /*System                 */          
               left(EEVETKSB,08) ||,       /*SubSystem              */          
               left(LNElmNam,10) ||,       /*Element (10)           */          
               left(EEVETKTY,08)           /*Type                   */          
      SName = 'S' LEFT('S'||Substr(EleKey,2,17),44)/* Sys Enq Shrd  */          
      EName = 'E' EleKey                   /* Enq request Parm      */          
      DName = 'D' EleKey                   /* Deq request Parm      */          
      XName = 'D' Substr(SName,3)          /* System DeQueue (SHRD) */          
      ADDRESS LINKMVS 'CTLIENQ SName'      /* Try Enq for System    */          
      If rc > 0 then                       /* System being back'd up*/          
      DO                                                                        
        EEVSYTSY = EEVETKSY                /* system code for mesg  */          
        ADDRESS ISPEXEC "SETMSG MSG(ENDE057E)"                                  
        EEVETDMS = "*Locked"                                                    
        FGAPIRC = 16                                                            
        return                                                                  
      END                                                                       
      ADDRESS LINKMVS 'CTLIENQ EName'      /* Try Enqueue Now       */          
      If rc > 0 then                       /* Element in use        */          
      DO                                                                        
        ADDRESS ISPEXEC "SETMSG MSG(ENDE056E)"                                  
        EEVETDMS = "*InUse"                                                     
        FGAPIRC = 16                                                            
        return                                                                  
      END                                                                       
   end                                                                          
                                                                                
                                                                                
   If SRCTYP = "ASCII" ,                                                        
    | SRCTYP = "UTF8" then                                                      
      call bpxwunix "mkdir -p -m 755 '"LongTempDir"'"                           
   Else do                                                                      
      Call Get_Type_LRECL_and_BLKSIZE ;                                         
      ADDRESS TSO,                                                              
      "ALLOC F("LONGDDPR"SRCE) LRECL("LRECL") BLKSIZE("BLKSIZE") SPACE(5,5) ",  
       "DSORG(PS)",                                                             
        "RECFM("RECFM") TRACKS NEW CATALOG REUSE " ;                            
   end                                                                          
                                                                                
   LNLASACT = 'Retrieve'                                                        
   sa=SAVESCL(RESET)                    /* reset our cache of SCL lines */      
   LNLASTGT = EEVETKSI                  /* Target Stage for action */           
   LNLASTYP = EEVETKTY                  /* Target Type for action  */           
   LNLASPRM = N                         /* Promote Eligible Flag N */           
   sa=SAVESCL(" RETRIEVE ELEMENT")                                              
   sa=SAVESCL("'"EEVETKEL"'")                                                   
   IF LENGTH(Selected_EEVETDVL) = 4 THEN,                                       
      sa=SAVESCL("  VERSION" SUBSTR(Selected_EEVETDVL,1,2),                     
               "LEVEL " SUBSTR(Selected_EEVETDVL,3,2))                          
   ELSE,                                                                        
      sa=SAVESCL("*  Current Version Level")                                    
   sa=SAVESCL("  FROM ENVIRONMENT "EEVETKEN " SYSTEM "EEVETKSY)                 
   sa=SAVESCL("    SUBSYSTEM "EEVETKSB " TYPE "EEVETKTY " STAGE" EEVETKSI)      
   If SRCTYP = "ASCII" ,                                                        
    | SRCTYP = "UTF8" then do                                                   
      sa=SAVESCL("  TO PATH '"LongTempDir"/' HFSFILE ")                         
      sa=SAVESCL("'"EEVETKEL"'")                                                
      end                                                                       
   Else                                                                         
      sa=SAVESCL("  TO DDNAME '"LONGDDPR"SRCE' ")                               
   sa=SAVESCL("  OPTIONS NO SIGNOUT COMMENT 'Long-Name' REPLACE.")              
 /*sa=SAVESCL("  ")*/                                                           
   my_parms = SAVESCL(GETALL)           /* retrieve the accumulated SCL */      
   CALL Execute_API_SCL ;                                                       
   return                                                                       
                                                                                
SignIn_Element_TSO :                                                            
                                                                                
   if EEVOOSGN /= "Y" &,                                                        
      EEVETSO  /= '' &,                                                         
      USERID() /= EEVETSO THEN,                                                 
      DO                                                                        
      ADDRESS ISPEXEC "SETMSG MSG(LONG010I)" ;                                  
      RETURN                                                                    
      END                                                                       
                                                                                
   LNLASACT = 'Signin'                                                          
   sa=SAVESCL(RESET)                    /* reset our cache of SCL lines */      
   LNLASTGT = EEVETKSI                  /* Target Stage for action */           
   LNLASTYP = EEVETKTY                  /* Target Type for action  */           
   LNLASPRM = N                         /* Promote Eligible Flag N */           
   sa=SAVESCL(" SIGNIN ELEMENT ")                                               
   sa=SAVESCL("'"EEVETKEL"'")                                                   
   sa=SAVESCL("  FROM ENVIRONMENT "EEVETKEN " SYSTEM "EEVETKSY)                 
   sa=SAVESCL("    SUBSYSTEM "EEVETKSB " TYPE "EEVETKTY " STAGE" EEVETKSI)      
   IF EEVOOSGN = "Y" then Options = "OVERRIDE SIGNOUT";                         
   else Options = "";                                                           
   sa=SAVESCL("  OPTIONS CCID '"EEVCCID"'" Options)                             
   IF index(EEVCOMM,"'") > 0 then /* if comment has an apost */                 
     sa=SAVESCL('          COMMENT "'EEVCOMM'".')                               
   else                                                                         
     sa=SAVESCL("          COMMENT '"EEVCOMM"'.")                               
 /*sa=SAVESCL("  ")*/                                                           
   my_parms = SAVESCL(GETALL)           /* retrieve the accumulated SCL */      
   CALL Execute_API_SCL ;                                                       
                                                                                
   If FGAPIRC < 12  THEN DO                                                     
      EEVETDMS = "*SignedIn"                                                    
      EEVETSO  = ''                                                             
      End                                                                       
   Else                                                                         
      EEVETDMS = "*Error"                                                       
                                                                                
   return;                                                                      
                                                                                
Execute_API_SCL:                                                                
                                                                                
/* testing - consider keeping the APIMSG/APILIST for debugging                  
   ADDRESS TSO "ALLOC F(APIMSGS)  DUMMY REUSE"                                  
   ADDRESS TSO "ALLOC F(APILIST)  DUMMY REUSE"                                  
   */                                                                           
   ADDRESS TSO,                                                                 
   "ALLOC F(APIMSGS) LRECL(133) BLKSIZE(26600) SPACE(5,5) ",                    
     "DSORG(PS) RECFM(V B) TRACKS NEW CATALOG REUSE " ;                         
   ADDRESS TSO,                                                                 
/* Note: the APILIST is not required, IF the APIPGM doesn't refer to it...      
   "ALLOC F(APILIST) LRECL(133) BLKSIZE(26600) SPACE(5,5) ",                    
     "DSORG(PS) RECFM(V B) TRACKS NEW CATALOG REUSE "                           
   */                                                                           
   /* delete any preexisting files */                                           
   CALL OUTTRAP "out."                                                          
      "FREE  F(C1MSGS1)"                                                        
      "DELETE '"LongDsPrefix".MSG1'"                                            
   CALL OUTTRAP "OFF"                                                           
                                                                                
   "ALLOC F(C1MSGS1) LRECL(133) BLKSIZE(26600) SPACE(5,5) ",                    
   "DA('"LongDsPrefix".MSG1')",                                                 
    "DSORG(PS)",                                                                
     "RECFM(V B) TRACKS MOD CATALOG REUSE " ;                                   
                                                                                
   "ALLOC FI(SYSOUT)  DUMMY SHR REUSE"                                          
   ADDRESS LINKMVS 'APIAESCL MY_PARMS'                                          
   FGAPIRC = RC ;                                                               
   if FGAPIRC = 20 then,                                                        
      do                                                                        
      ADDRESS LINKMVS 'APIAESCL' 'MY_PARMS'                                     
      FGAPIRC = RC ;                                                            
      end                                                                       
   If FGAPIRC = 17  THEN Say "Maximum API parm size 2000bytes, exceeded",       
                             "This request was" length(MY_PARMS)                
   If FGAPIRC > 4   THEN,                                                       
      Do                                                                        
      EEVEDTIT = 'API Action Error Report - RC:' FGAPIRC                        
      ADDRESS ISPEXEC "SETMSG MSG(LONG027C) COND" ;                             
      ADDRESS ISPEXEC "LMINIT DATAID(DDMS) DDNAME(C1MSGS1)"                     
      VARC1LR = ''       /* Allow standard Left/Right  */                       
      ADDRESS ISPEXEC "VIEW   DATAID(&DDMS) PANEL(ISREDDHI) MACRO(ENDVHISC)"    
      VARC1LR = PASSTHRU /* enable Left/Right commands */                       
      ADDRESS ISPEXEC "LMFREE DATAID(&DDMS)"                                    
      End;                                                                      
                                                                                
   RETURN;                                                                      
                                                                                
DETERMINE_Unique_Name:                                                          
                                                                                
   NUMBERS    = '123456789' ;                                                   
   CHARACTERS = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ' ;                                  
   TODAY = DATE(O) ;                                                            
   YEAR = SUBSTR(TODAY,1,2) + 1;                                                
   YEAR = SUBSTR(CHARACTERS||CHARACTERS||CHARACTERS||CHARACTERS,YEAR,1)         
   MONTH = SUBSTR(TODAY,4,2) ;                                                  
   MONTH = SUBSTR(CHARACTERS,MONTH,1) ;                                         
   DAY = SUBSTR(TODAY,7,2) ;                                                    
                                                                                
   DAY = SUBSTR(CHARACTERS || NUMBERS,DAY,1) ;                                  
   NOW  = TIME() ;                                                              
   HOUR = SUBSTR(NOW,1,2) ;                                                     
   IF HOUR = '00' THEN HOUR = '0'                                               
   ELSE                                                                         
   HOUR = SUBSTR(CHARACTERS,HOUR,1) ;                                           
   MINUTE = SUBSTR(NOW,4,2) ;                                                   
   SECOND = SUBSTR(NOW,7,2) ;                                                   
   Unique_Name = YEAR || MONTH || DAY || HOUR ||,                               
         MINUTE || SECOND ;                                                     
   SA= "UNIQUE MEMBER NAME IS " Unique_Name                                     
                                                                                
   RETURN ;                                                                     
                                                                                
Get_Type_LRECL_and_BLKSIZE:                                                     
                                                                                
Check_Local_OverRides:                                                          
                                                                                
   tmp = WORDPOS(EEVETKTY,Non_80byte_Types) ; /* do we have an override */      
   if tmp \= 0 then do                        /* Yes... decode it       */      
      lb = TRANSLATE(Word(Non_80byte_Lengths,tmp)," ","/")                      
      LRECL   = WORD(lb,1)                                                      
      BLKSIZE = WORD(lb,2)                                                      
      RECFM = ''  /* Reset the record Format and add space delimiters   */      
      do i = 1 to length(Word(Non_80byte_recfms,tmp))                           
         RECFM = RECFM SUBSTR(Word(Non_80byte_recfms,tmp),i,1)                  
      end                                                                       
      return                                 /* use local values        */      
   end                                                                          
                                                                                
   /* If we're still here we need to check our cache and if not found           
      call the CSV utility to get the details for this system           */      
                                                                                
   if TypeLen.EEVETKEN.EEVETKSY.EEVETKTY = "" then /* Check Cache 4 type*/      
      Call Get_CSV_for_LRECL_Recfm          /* not found? get CSV       */      
                                                                                
   ThisLen = TypeLen.EEVETKEN.EEVETKSY.EEVETKTY /* Element Source Len   */      
   ThisRFM = TypeRFM.EEVETKEN.EEVETKSY.EEVETKTY /* Record fmt F/b/n     */      
   BLKSIZE = 0                              /* Let System work it out   */      
   select                                                                       
      when ThisRFM = 'N' & ThisLen = 80 then do                                 
         LRECL = 80             /* Default FB80 catch all           */          
         RECFM   = 'F B'                                                        
         end                                                                    
      when ThisRFM = 'N' & ThisLen > 80 then do                                 
         LRECL = ThisLen + 4    /* Variable length, add 4 for RDW   */          
         RECFM   = 'V B'                                                        
         end                                                                    
      when ThisRFM = 'F' then do                                                
         LRECL = ThisLen        /* Fixed Length, assume blocked     */          
         RECFM   = 'F B'                                                        
         end                                                                    
      when ThisRFM = 'V' then do                                                
         LRECL = ThisLen + 4    /* Variable length, add 4 for RDW   */          
         RECFM   = 'V B'                                                        
         end                                                                    
      otherwise                                                                 
   end                                                                          
   RETURN ;                                                                     
                                                                                
Get_CSV_for_LRECL_Recfm:                                                        
                                                                                
   CALL OUTTRAP "out."                                                          
      "FREE  F("LONGDDPR"CSVD)"                                                 
      "DELETE '"LongDsPrefix".CSVD'"                                            
      "FREE  F(C1MSGS1)"                                                        
      "DELETE '"LongDsPrefix".MSG1'"                                            
      "FREE  F(C1MSGSA)"                                                        
      "DELETE '"LongDsPrefix".MSGA'"                                            
   CALL OUTTRAP "OFF"                                                           
                                                                                
   "ALLOC F(C1MSGS1) LRECL(133) BLKSIZE(26600) SPACE(5,5) ",                    
   "DA('"LongDsPrefix".MSG1')",                                                 
    "DSORG(PS)",                                                                
     "RECFM(V B) TRACKS MOD CATALOG REUSE " ;                                   
                                                                                
   "ALLOC F(C1MSGSA) LRECL(133) BLKSIZE(26600) SPACE(5,5) ",                    
   "DA('"LongDsPrefix".MSGA')",                                                 
    "DSORG(PS)",                                                                
     "RECFM(V B) TRACKS mod CATALOG REUSE " ;                                   
                                                                                
   "ALLOC F("LONGDDPR"CSVD) LRECL(4096) BLKSIZE(0) SPACE(5,10) ",               
   "DA('"LongDsPrefix".CSVD')",                                                 
    "DSORG(PS)",                                                                
     "RECFM(V B) CYL MOD CATALOG REUSE " ;                                      
                                                                                
   "ALLOC FI(BSTIPT01) BLKSIZE(0) TRACKS LRECL(80) SPACE(5 5)",                 
    "DSORG(PS)",                                                                
       "RECFM(F B) NEW REUSE UNCATALOG" ;                                       
                                                                                
                                                                                
   sa=SAVESCL(RESET)                    /* reset our cache of SCL lines */      
   /* Probaly worth getting ALL the types for this ENV/SYS...                   
   sa=SAVESCL(" LIST TYPE '"EEVETKTY"' TO DDNAME '"LONGDDPR"CSVD'")             
   */                                                                           
   sa=SAVESCL(" LIST TYPE '*' TO DDNAME '"LONGDDPR"CSVD'")                      
   if ENT.EEVETKEN /= ""  then do /* if we no the entry stage use it */         
      sa=SAVESCL("  FROM ENV" EEVETKEN,                                         
                      "SYS" EEVETKSY,                                           
                      "STAGE" ENT.EEVETKEN)                                     
      sa=SAVESCL("  OPTIONS DELIMITERS 'º' NOSEARCH.")                          
   end                                                                          
   else do                        /* otherwise just find first       */         
      sa=SAVESCL("  FROM ENV" EEVETKEN,                                         
                      "SYS" EEVETKSY,                                           
                      "STAGE '*'")                                              
      sa=SAVESCL("  OPTIONS DELIMITERS 'º' SEARCH RETURN FIRST.")               
   end                                                                          
   sa=SAVESCL(EXECIO, BSTIPT01)         /* write our cache to temp file */      
                                                                                
   /*                                                                  */       
   /* Now call the Endevor CSV utility to find all the matching        */       
   /* types    - note a return code of -3 means the module wasn't      */       
   /* found - add a "libdef islpplib dataset id(...conlib) stack"      */       
   /*                                                                  */       
   ADDRESS LINK 'BC1PCSV0'   ;  /* Try load from authlib or linklist?  */       
   APIRC = rc ;                 /* save the API response code          */       
   If apirc = -3 then do        /* if module not found in STEPLIB or   */       
                                /* ISPLLIB then try loading from conlib*/       
      ndvrprm = 'CONCALL,DDN:CONLIB,BC1PCSV0'   /* set up parm for CSV */       
      address TSO "call *(NDVRC1)  '"ndvrprm"'" /* need to use TSO for */       
      APIRC = rc ;                              /* auth pgm call       */       
   end                                                                          
   if apirc <  0,               /* Negative RC means something bad     */       
    | apirc > 12 then           /* but 4, 8 and 12 are normal          */       
      Sa= "Unexpected RC:" apirc,                                               
      "from BC1PCSV0 List Types routine",                                       
      "Please contact your Endevor Administrator"                               
                                                                                
   "EXECIO * DISKR "LONGDDPR"CSVD (STEM API. finis"                             
   /* "FREE F("LONGDDPR"CSVD)" */                                               
   "FREE F(BSTIPT01)"                                                           
   /* Debug CSV output ?                                                        
      ADDRESS ISPEXEC "LMINIT DATAID(DDID) DDNAME("LONGDDPR"CSVD)"              
      ADDRESS ISPEXEC "EDIT DATAID(&DDID)"                                      
      ADDRESS ISPEXEC "LMFREE DATAID(&DDID)"                                    
   */                                                                           
                                                                                
   IF API.0 < 2 THEN do         /* No data is BAD!      */                      
      Say "No Type records found for Env:" EEVETKEN,                            
          "Sys:" EEVETKSY "- check messages... defaulting to FB80"              
      TypeLen.EEVETKEN.EEVETKSY.EEVETKTY = 80                                   
      TypeRFM.EEVETKEN.EEVETKSY.EEVETKTY = N                                    
      return                                                                    
   end                                                                          
                                                                                
   /* Parse the CSV records looking for the columns we need */                  
                                                                                
   do I = 1 to dwords(API.1)    /* for each heading...  */                      
      thisword = dword(API.1,i)                                                 
      select                                                                    
         when thisword == 'ENV NAME'      then PosEnv = i                       
         when thisword == 'SYS NAME'      then PosSys = i                       
         when thisword == 'TYPE NAME'     then PosTyp = i                       
         when thisword == 'SRC LNG'       then PosLen = i                       
         when thisword == 'ELEMENT RECFM' then PosRFM = i                       
         otherwise iterate                                                      
      end                                                                       
   end                                                                          
                                                                                
   do I = 2 to API.0            /* for each type record found */                
      ThisEnv = strip(dWORD(API.I,PosEnv))                                      
      ThisSys = strip(dWORD(API.I,PosSys))                                      
      ThisTyp = strip(dWORD(API.I,PosTyp))                                      
      ThisLen = strip(dWORD(API.I,PosLen))                                      
      ThisRFM = strip(dWORD(API.I,PosRFM))                                      
      sa= "Found:" ,            /* debug */                                     
           ThisEnv ,                                                            
           ThisSys ,                                                            
           ThisTyp ,                                                            
           ThisLen ,                                                            
           ThisRFM                                                              
      TypeLen.ThisEnv.ThisSys.ThisTyp = strip(ThisLen,'L',0)                    
      TypeRFM.ThisEnv.ThisSys.ThisTyp = ThisRFM                                 
   end                                                                          
                                                                                
   RETURN ;                                                                     
                                                                                
Show_JobCard:                                                                   
                                                                                
   ADDRESS ISPEXEC "VGET (EMULTREQ EMULTGT EMULPRM EMULTYPP) PROFILE"           
   ADDRESS ISPEXEC "VGET (C1BJC1 C1BJC2 C1BJC3 C1BJC4) PROFILE "                
   ADDRESS ISPEXEC "VGET (LNBJC1 LNBJC2 LNBJC3 LNBJC4) PROFILE "                
   ADDRESS ISPEXEC "VGET (LNBJC5 LNBJC6 LNBJC7 LNBJC8) PROFILE "                
   IF LNBJC1 = '' THEN LNBJC1 = C1BJC1                                          
   IF LNBJC2 = '' THEN LNBJC2 = C1BJC2                                          
   IF LNBJC3 = '' THEN LNBJC3 = C1BJC3                                          
   IF LNBJC4 = '' THEN LNBJC4 = C1BJC4                                          
   IF LNBJC5 = '' THEN LNBJC5 = '//*'                                           
   IF LNBJC6 = '' THEN LNBJC6 = '//*'                                           
   IF LNBJC7 = '' THEN LNBJC7 = '//*'                                           
   IF LNBJC8 = '' THEN LNBJC8 = '//*'                                           
   ADDRESS ISPEXEC "VGET (EMULTCNT) PROFILE" /* any active requests?        */  
   IF EMULTCNT = '' then EMULTCNT = 0        /* first time?                 */  
   EMULTPND = SCLLINE.0                      /* how many lines pending?     */  
   EMULTTOT = EMULTPND + EMULTCNT + 1        /* + lines in table + header   */  
   ADDRESS ISPEXEC "VPUT (EMULTTOT EMULTPND) shared" /* share this for panel*/  
   if EMULTREQ > 0 then                                                         
      ADDRESS ISPEXEC "SETMSG MSG(LONG020I) COND" ; /* Pending reqs!        */  
   VARWKCMD = ''                             /* reset any command */            
   ADDRESS ISPEXEC "DISPLAY PANEL(LONGSUBJ) "                                   
   JobCard_RC = RC                                                              
   ADDRESS ISPEXEC "VPUT (LNBJC1 LNBJC2 LNBJC3 LNBJC4) PROFILE "                
   ADDRESS ISPEXEC "VPUT (LNBJC5 LNBJC6 LNBJC7 LNBJC8) PROFILE "                
                                                                                
   if VARWKCMD == 'CANCEL' ,        /* Did user Cancel?     */                  
    | VARWKCMD == 'CAN' Then Do                                                 
      if EMULTREQ > 0 then do       /* did we have any saved requests? */       
         call clear_Request_queue   /* Clear request queue */                   
         call Clear_Request_Count   /* & reset the pending req count */         
         ADDRESS ISPEXEC "SETMSG MSG(LONG029C)" ; /* request cancelled      */  
         return                     /* and return to user   */                  
      end                                                                       
      else                                                                      
         JobCard_RC = 8             /* act as if END or RETURN pressed */       
   end                                                                          
                                                                                
   if JobCard_RC > 0 then do     /* Did user Cancel?     */                     
      EEVETDMS = "*Canceled"     /* change the message   */                     
      return                     /* and return to user   */                     
   end                                                                          
                                                                                
   /* if user didn't cancel - save the scl in a table...                  */    
   Reqs# =SAVESCL(ADD2TAB)      /* add these cards and get how many reqs? */    
   ADDRESS ISPEXEC "VGET (EMULTREQ EMULTGT EMULPRM EMULTYPP) PROFILE"           
                                                                                
   ADDRESS ISPEXEC "VGET (ENABSUBJ) PROFILE"  /* what next? Edit, save,etc. */  
                                                                                
   If index(EEVETDMS,'/') > 0 then          /* Did we have a status?     */     
      EEVETDMS = left(EEVETDMS,index(EEVETDMS,'/')) || "Que'd"                  
                                                                                
   If ENABSUBJ == 'APPEND' then             /* user want to stack the SCL*/     
      ADDRESS ISPEXEC "SETMSG MSG(LONG029I)" ; /* request saved          */     
                                                                                
   If ENABSUBJ == 'EDIT' ,                  /* user want to edit the JCL */     
    | ENABSUBJ == 'SUBMIT' then             /* Or submit it...           */     
     Call Sub_Incr_JobCard;                                                     
                                                                                
   If ENABSUBJ == 'PACKAGE' then            /* User want to Package SCL? */     
      call Cre_Pkg_Req                      /* Try to create a package   */     
                                                                                
   If ENABSUBJ == 'F/G' then do             /* Try Foreground...         */     
      my_parms = ''                         /* clear parms */                   
      ADDRESS ISPEXEC "TBTOP    EMULTTAB"   /* go to top                 */     
      do forever                            /* For each saved req        */     
         ADDRESS ISPEXEC "TBSKIP   EMULTTAB"   /* get next               */     
         if rc = 0 then                        /* we got one?            */     
            my_parms=my_parms||LEFT(SCLLINE,80) /* append the SCL line    */    
         else                                                                   
            leave                                                               
      end                                                                       
      CALL Execute_API_SCL ;                                                    
      call clear_Request_queue             /* Clear request queue */            
      call Clear_Request_Count             /* & reset the pending req count */  
      If index(EEVETDMS,'/') > 0 then      /* Did we have a status?     */      
         EEVETDMS = left(EEVETDMS,index(EEVETDMS,'/')) || "RC:" || FGAPIRC      
      if FGAPIRC >  8 then do                                                   
         ENABAUTO = 'N'                      /* TurnOff auto submit         */  
         ADDRESS ISPEXEC "VPUT (ENABAUTO) Shared"  /* and save for LONGSUBJ */  
         ADDRESS ISPEXEC "SETMSG MSG(LONG023E)"/* F/G failed, try batch?    */  
      end                                                                       
   end                                                                          
   return                                   /* request complete          */     
                                                                                
Cre_Pkg_Req:                     /* This routine will prompt the user for       
                                    package details required (Name, Description 
                                    notes, execution window etc. and then create
                                    the package and import the currently queued 
                                    scl requests. */                            
Build_Package: /* call ENBP1000 to actually create the package */               
   /* Note a lot of the following code is based on PACKAGE NoBrainR             
      (Thanks Dan) so the style is slightly different and it makes              
      more use of the wherami functions that might not be set, but              
      if set at a site should enable LongPack to play nicely there too.         
      */                                                                        
                                                                                
/* Variable settings for each site --->           */                            
   WhereIam =  WHERE@M1()                                                       
                                                                                
   interpret 'Call' WhereIam "'SchedulingPackageShipBundle'"                    
   SchedulingPackageShipBundle = Result                                         
                                                                                
   interpret 'Call' WhereIam "'SchedulingOption'"                               
   SchedulingOption = Result                                                    
                                                                                
   interpret 'Call' WhereIam "'ShipSchedulingMethod'"                           
   ShipSchedulingMethod = Result                                                
                                                                                
   interpret 'Call' WhereIam "'MyCLS2Library'"                                  
   HSYSEXEC  = Result                                                           
   MyCLS2Library = HSYSEXEC                                                     
                                                                                
   interpret 'Call' WhereIam "'MyDATALibrary'"                                  
   MyDATALibrary = Result                                                       
   ShipRules       = MyDATALibrary"(SHIPRULE)"                                  
                                                                                
   ADDRESS ISPEXEC "VGET (EMULTGT EMULPRM EMULTYPP) PROFILE"                    
/* Generate a unique suffix like PACKAGE */                                     
   call Build_Package_Suffix                                                    
   call Calculate_Date_Fields                                                   
   package = ""    /* clear default package */                                  
   System_List = Strip(System_List) ;                                           
   If ShipSchedulingMethod = 'Notes' then,                                      
      Call Build_NOTES_Fields   ;                                               
   If ShipSchedulingMethod = 'Rules' then nop                                   
   VPHNOTE8 = SchedulingOption'>' BTSTDATE '00:00' ;                            
   /* delete any preexisting files */                                           
   ADDRESS TSO;                                                                 
   CALL OUTTRAP "out."                                                          
      "FREE  F(C1MSGS1)"                                                        
      "DELETE '"LongDsPrefix".MSG1'"                                            
      "FREE  F(PKGESCL)"                                                        
      "DELETE '"LongDsPrefix".SCLP'"                                            
   CALL OUTTRAP "OFF"                                                           
                                                                                
   "ALLOC F(C1MSGS1) LRECL(133) BLKSIZE(26600) SPACE(5,5) ",                    
   "DA('"LongDsPrefix".MSG1')",                                                 
    "DSORG(PS)",                                                                
     "RECFM(V B) TRACKS MOD CATALOG REUSE " ;                                   
                                                                                
                                                                                
   "ALLOC FI(PKGESCL)  BLKSIZE(0) TRACKS LRECL(80) SPACE(5 5)",                 
   "DA('"LongDsPrefix".SCLP')",                                                 
    "DSORG(PS)",                                                                
       "RECFM(F B) NEW REUSE CATALOG" ;                                         
                                                                                
/* All the SCL has been saved in a table - save it to a temp file to include    
   or edit as required.                                                         
   */                                                                           
   ADDRESS ISPEXEC "VGET (EEVCCID EEVCOMM)" /*incase they changed*/             
   queue "SET STOPRC 16."                                                       
   if EEVCCID \= '' then                                                        
      queue 'SET OPTION CCID "'EEVCCID'".'                                      
   if EEVCOMM \= '' then                                                        
      queue 'SET OPTION COMMENT "'EEVCOMM'".'                                   
   ADDRESS ISPEXEC "TBTOP    EMULTTAB"   /* go to top                 */        
   do forever                            /* For each saved req        */        
      ADDRESS ISPEXEC "TBSKIP   EMULTTAB"   /* get next               */        
      if rc = 0 then                        /* we got one?            */        
         queue LEFT(SCLLINE,80)           /* append the SCL line    */          
      else                                                                      
         leave                                                                  
   end                                                                          
                                                                                
   ADDRESS TSO,                         /* write queued lines to file */        
      "EXECIO" QUEUED() "DISKW PKGESCL  (FINIS " ;                              
                                                                                
   ADDRESS TSO,                                                                 
      "ALLOC F(ENPSCLIN)",                                                      
          "LRECL(80) BLKSIZE(0)   SPACE(5,5)",                                  
          "RECFM(F B) TRACKS ",                                                 
          "NEW UNCATALOG REUSE "     ;                                          
                                                                                
Display_prompt:                                                                 
   Do Forever                                                                   
      address ispexec "display panel(longpack)"                                 
      disprc = RC                                                               
      if disprc > 0 then                                                        
         leave                                                                  
   /* did user request edit of SCL lines? */                                    
      if PICKLIST = "Y" then do                                                 
         ADDRESS ISPEXEC "LMINIT DATAID(DDID) DDNAME(PKGESCL)"                  
         ADDRESS ISPEXEC "EDIT DATAID(&DDID)",                                  
                      "Profile(SCL)"                                            
         ADDRESS ISPEXEC "LMFREE DATAID(&DDID)"                                 
         PICKLIST = "N"                                                         
      end                                                                       
      else                                                                      
        leave /* user just pressed enter and we're ready to go */               
   end                                                                          
   /* Check what user wants to do, just press enter to continue otherwise, */   
   address ispexec "vget (zverb)"           /* Cancel, End or Return?      */   
   IF dispRC >= 8 ,                         /* End or Return key pressed?  */   
    | ZVERB == 'CANCEL' THEN do             /* or entered cancel command?  */   
      ADDRESS ISPEXEC "SETMSG MSG(LONG028W)" ; /* Package Create Cancelled */   
      RETURN                      /* go back now - do not execute ENBP1000 */   
   end                                                                          
                                                                                
   /* Note using the define, import SCL will update an existing package and     
      replace any SCL that was pre-existing - if the packag is in edit status   
      If the package already exists in cast status you'll get RC:12             
      */                                                                        
                                                                                
   QUEUE " DEFINE PACKAGE '"PACKAGE"'"                                          
   QUEUE "        IMPORT SCL FROM DDNAME 'PKGESCL'"                             
   If pos('"',DESCRIPT) > 0 then    /* if comment has a Quote  */               
     QUEUE "  DESCRIPTION '"DESCRIPT"'" /* wrap in Aposts      */               
   else                             /* otherwise try quotes    */               
     QUEUE '  DESCRIPTION "'DESCRIPT'"'                                         
   QUEUE "        OPTIONS STANDARD SHARABLE BACKOUT ENABLED "                   
   QUEUE "          EXECUTION WINDOW FROM " BTSTDATE BTSTTIME                   
   QUEUE "                           TO   " BTENDATE BTENTIME                   
   If PROMOTE = "Y" & ACTION = 'MOVE' then,                                     
      QUEUE "                PROMOTION PACKAGE "                                
   If pos('"',VPHNOTE1) > 0 then    /* if Note1   has a Quote  */               
      QUEUE " NOTES=('"VPHNOTE1"',"                                             
   else                             /* otherwise try quotes    */               
      QUEUE ' NOTES=("'VPHNOTE1'",'                                             
   If pos('"',VPHNOTE2) > 0 then    /* if Note2   has a Quote  */               
      QUEUE "        '"VPHNOTE2"',"                                             
   else                             /* otherwise try quotes    */               
      QUEUE '        "'VPHNOTE2'",'                                             
   If pos('"',VPHNOTE3) > 0 then    /* if Note3   has a Quote  */               
      QUEUE "        '"VPHNOTE3"',"                                             
   else                             /* otherwise try quotes    */               
      QUEUE '        "'VPHNOTE3'",'                                             
   If pos('"',VPHNOTE4) > 0 then    /* if Note4   has a Quote  */               
      QUEUE "        '"VPHNOTE4"',"                                             
   else                             /* otherwise try quotes    */               
      QUEUE '        "'VPHNOTE4'",'                                             
   If pos('"',VPHNOTE5) > 0 then    /* if Note5   has a Quote  */               
      QUEUE "        '"VPHNOTE5"',"                                             
   else                             /* otherwise try quotes    */               
      QUEUE '        "'VPHNOTE5'",'                                             
   If pos('"',VPHNOTE6) > 0 then    /* if Note6   has a Quote  */               
      QUEUE "        '"VPHNOTE6"',"                                             
   else                             /* otherwise try quotes    */               
      QUEUE '        "'VPHNOTE6'",'                                             
   If pos('"',VPHNOTE7) > 0 then    /* if Note7   has a Quote  */               
      QUEUE "        '"VPHNOTE7"',"                                             
   else                             /* otherwise try quotes    */               
      QUEUE '        "'VPHNOTE7'",'                                             
   If pos('"',VPHNOTE8) > 0 then    /* if Note8   has a Quote  */               
      QUEUE "        '"VPHNOTE8"') . "                                          
   else                             /* otherwise try quotes    */               
      QUEUE '        "'VPHNOTE8'") . '                                          
   ADDRESS TSO,                                                                 
      "EXECIO" QUEUED() "DISKW ENPSCLIN (FINIS " ;                              
                                                                                
   ADDRESS LINK "ENBP1000" ;  /* Note this should be in Authlib/linklist */     
   ENBPRC = RC                                                                  
                                                                                
   ADDRESS TSO;               /* free our temp files anyway */                  
   CALL OUTTRAP "out."                                                          
      "FREE  F(ENPSCLIN)"                                                       
      "FREE  F(PKGESCL)"                                                        
      "DELETE '"LongDsPrefix".SCLP'"                                            
   CALL OUTTRAP "OFF"                                                           
                                                                                
   IF ENBPRC > 4 ,                                                              
    | ENBPRC < 0 THEN DO                                                        
      ADDRESS ISPEXEC "SETMSG MSG(LONG028E)" ; /* Package Create failed */      
      ADDRESS ISPEXEC "LMINIT DATAID(DDID) DDNAME(C1MSGS1)"                     
      ADDRESS ISPEXEC "VIEW DATAID(&DDID) PANEL(ISREDDHI) MACRO(ENDVHISC)"      
      ADDRESS ISPEXEC "LMFREE DATAID(&DDID)"                                    
   End                                                                          
   else do                                                                      
      Sa= "Package Built OK..."PACKAGE     /* Package Created */                
      call clear_Request_queue             /* Clear request queue */            
      call Clear_Request_Count             /* & reset the pending req count */  
      Sa= "Request Queue Cleared"                                               
                                                                                
      /* now call routine to submit batch bits, cast, validate, execute... */   
      call CAST_Package                                                         
   End                                                                          
                                                                                
return                                                                          
                                                                                
/* this section builds the default package name... */                           
                                                                                
/*    No additional changes are required.         */                            
/*       However, if you wish to modify the       */                            
/*       structure of package names, find below   */                            
/*      "Examples for  building the package name" */                            
Build_Package_Suffix:                                                           
                                                                                
   NUMBERS    = '123456789' ;                                                   
   CHARACTERS = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ' ;                                  
   TODAY = DATE(O) ;                                                            
   YEAR = SUBSTR(TODAY,1,2) + 1;                                                
   YEAR = SUBSTR(CHARACTERS||CHARACTERS||CHARACTERS||CHARACTERS,YEAR,1)         
   MONTH = SUBSTR(TODAY,4,2) ;                                                  
   MONTH = SUBSTR(CHARACTERS,MONTH,1) ;                                         
   DAY = SUBSTR(TODAY,7,2) ;                                                    
   DAY = SUBSTR(CHARACTERS || NUMBERS,DAY,1) ;                                  
   NOW  = TIME(L);                                                              
   HOUR = SUBSTR(NOW,1,2) ;                                                     
   IF HOUR = '00' THEN HOUR = '0'                                               
   ELSE                                                                         
   HOUR = SUBSTR(CHARACTERS,HOUR,1) ;                                           
                                                                                
   MINUTE = SUBSTR(NOW,4,2) ;                                                   
   SECOND = SUBSTR(NOW,7,2) ;                                                   
   Fractn = SUBSTR(NOW,10,6) ;                                                  
   PKGUNIQ = YEAR || MONTH || DAY || HOUR ||,                                   
         MINUTE || SECOND || Fractn ;                                           
                                                                                
Return ;                                                                        
                                                                                
Calculate_Date_Fields:                                                          
                                                                                
  GENERATE = "Y" ;                                                              
                                                                                
                                                                                
  TEMP     = DATE('N')                                                          
  DAY      = WORD(TEMP,1)                                                       
  IF LENGTH(DAY) < 2 THEN DAY = "0"DAY ;                                        
  MON      = WORD(TEMP,2)                                                       
  YEAR     = SUBSTR(WORD(TEMP,3),3,2) ;                                         
  IF LENGTH(YEAR) < 2 THEN YEAR = "0"YEAR ;                                     
  BTSTDATE = DAY || MON || YEAR;                                                
  ONSTDATE = DAY || MON || YEAR;                                                
                                                                                
  TEMP     = TIME('N')                                                          
  BTSTTIME = SUBSTR(TEMP,1,5)                                                   
  BTSTTIME = "00:00"                                                            
                                                                                
  BTENDATE = "31DEC79"                                                          
  BTENTIME = "00:00"                                                            
                                                                                
  Return ;                                                                      
                                                                                
Build_NOTES_Fields:                                                             
                                                                                
  If SchedulingPackageShipBundle /= 'Y' then return;                            
  X = OUTTRAP(LINE.);                                                           
  DSNCHECK = SYSDSN("'"ShipRules"'") ;                                          
  IF DSNCHECK /= OK Then Return  ;                                              
                                                                                
  Tday = DATE('U');                                                             
  #days = DATE('B',Tday,'U');                                                   
   ADDRESS TSO,                                                                 
     "ALLOC F(TABLE) DA('"ShipRules"') SHR REUSE "                              
                                                                                
  ADDRESS TSO,                                                                  
     "EXECIO * DISKR TABLE (STEM $tbl. finis"                                   
   ADDRESS TSO,                                                                 
     "FREE  F(TABLE)"                                                           
  List_Destinations = " " ;                                                     
  Time_Destinations = " " ;                                                     
                                                                                
  /* Determine where critical columns are located */                            
  $heading = ' ' Substr($tbl.1,2) ;                                             
  $heading = Translate($heading,' ','-');                                       
  EnvironmentWrdPos = Wordpos('Environment',$heading);                          
  StageWrdPos       = Wordpos('Stage',$heading);                                
  SystemWrdPos      = Wordpos('System',$heading);                               
  DestinationWrdPos = Wordpos('Destination',$heading);                          
  DateWrdPos        = Wordpos('Date',$heading);                                 
  TimeWrdPos        = Wordpos('Time',$heading);                                 
  JobnameWrdPos     = Wordpos('Jobname',$heading);                              
  TyprunWrdPos      = Wordpos('Typrun',$heading);                               
  TypRunPosition    = Wordindex($heading,TyprunWrdPos);                         
                                                                                
  Do cnt# = 1 to Words(System_List)                                             
     srch = Word(System_list,cnt#) ;                                            
     Do tbl# = 2 to $tbl.0                                                      
        env = Word($tbl.tbl#,EnvironmentWrdPos)                                 
        stg = Word($tbl.tbl#,StageWrdPos)                                       
        sys = Word($tbl.tbl#,SystemWrdPos);                                     
        If (env =ProductionEnvironment | env = '*') &,                          
           (stg =ProductionStage | stg = '*') &,                                
           (sys = srch | sys = '*') then,                                       
           Do                                                                   
           Destination = Word($tbl.tbl#,DestinationWrdPos)                      
           If Wordpos(Destination,List_Destinations) > 0 then,                  
              iterate ;                                                         
           tmp         = Word($tbl.tbl#,DateWrdPos)                             
           tmp = Strip(tmp,'L','+') ;                                           
           Tday = DATE('U');                                                    
           #days = DATE('B',Tday,'U');                                          
           #days = #days + tmp ;                                                
           Date = DATE('S',#days,'B') ;                                         
           Time_Str    = Word($tbl.tbl#,TimeWrdPos)                             
           Jobname     = Word($tbl.tbl#,JobnameWrdPos)                          
           Hold_Str    = Strip(Substr($tbl.tbl#,TypRunPosition,8))              
           Time_Entry = Date"\"Time_Str"\"Hold_Str ;                            
           entry = Strip(Destination)'/'Strip(Jobname)                          
           List_Destinations = entry List_Destinations;                         
           Time_Destinations = Time_Entry Time_Destinations ;                   
           End;                                                                 
     End;  /* Do tbl# = 2 to $tbl.0  */                                         
  End ; /* Do cnt# = 1 to Words(System_List)  */                                
  VPHNOTE8 = " "                                                                
  Do cnt# = 1 to Words(List_Destinations)                                       
     Time_Entry = Word(Time_Destinations,cnt#) ;                                
     Time_Entry = Translate(Time_Entry," ","\") ;                               
     Date     = Word(Time_Entry,1) ;                                            
     Time_Str = Word(Time_Entry,2) ;                                            
     Hold_Str = Word(Time_Entry,3) ;                                            
     entry       = Word(List_Destinations,cnt#)                                 
     entry       = Translate(entry,' ','/')                                     
     Destination = Word(entry,1)                                                
     Jobname     = Word(entry,2)                                                
     nbr = 9 - cnt# ;                                                           
     If Hold_Str /= "HOLD" then Hold_Str = " "                                  
     tmp = "VPHNOTE"nbr" = 'To "Left(Destination,8)":",                         
        Date  Time_Str LEFT(Jobname,8) Hold_Str "'"                             
     interpret tmp ;                                                            
  End;                                                                          
                                                                                
  tmp = "Elm Cnt: "EMULTREQ                                                     
  VPHNOTE8 = Overlay(tmp,VPHNOTE8,48);                                          
                                                                                
  return;                                                                       
                                                                                
CAST_Package:                                                                   
                                                                                
  ADDRESS ISPEXEC "SETMSG MSG(CIUU021I)" ;                                      
  ADDRESS ISPEXEC "DISPLAY PANEL(ENDIE700) "                                    
  dispRC = rc                                                                   
  /* Check what user wants to do, just press enter to continue otherwise, */    
  address ispexec "vget (zverb)"           /* Cancel, End or Return?      */    
  IF dispRC >= 8 ,                         /* End or Return key pressed?  */    
   | ZVERB == 'CANCEL' THEN do             /* or entered cancel command?  */    
     ADDRESS ISPEXEC "SETMSG MSG(LONG028C)" ; /* Package Submit Cancelled */    
     RETURN                      /* go back now - do not execute ENBP1000 */    
  end                                                                           
                                                                                
  SCL_DSN = LongDsPrefix || "." || SUBSTR(PKGUNIQ,1,8)                          
                                                                                
  /* delete any preexisting files */                                            
  ADDRESS TSO;                                                                  
  CALL OUTTRAP "out."                                                           
     "FREE  dataset('"SCL_DSN"')"                                               
     "DELETE '"SCL_DSN"'"                                                       
  CALL OUTTRAP "OFF"                                                            
                                                                                
  ADDRESS TSO,                                                                  
  "ALLOC F(CASTSCL) DA('"SCL_DSN"')",                                           
         "LRECL(80) BLKSIZE(800) SPACE(5,5)",                                   
         "RECFM(F B) TRACKS ",                                                  
         "NEW CATALOG REUSE "     ;                                             
                                                                                
  If VALIDATE /= "Y" then,                                                      
     Do                                                                         
     QUEUE " CAST PACKAGE '"PACKAGE"'  "                                        
     QUEUE "    OPTION DO NOT VALIDATE COMPONENT ."                             
     End                                                                        
  Else,                                                                         
     Do                                                                         
     QUEUE " CAST PACKAGE '"PACKAGE"'  "                                        
     QUEUE "    OPTION        VALIDATE COMPONENT ."                             
     End                                                                        
  /*                                                                            
  */                                                                            
  If EXECUTE  = "Y" then,                                                       
     Do                                                                         
     QUEUE " EXECUTE PACKAGE '"PACKAGE"'  "                                     
     QUEUE "    OPTIONS WHERE PACKAGE STATUS IS APPROVED    . "                 
     End                                                                        
  ADDRESS TSO,                                                                  
     "EXECIO" QUEUED() "DISKW CASTSCL (FINIS " ;                                
  ADDRESS TSO "FREE  F(CASTSCL)"                                                
                                                                                
  PDVINCJC = "Y"                                                                
  PDVSCLDS = SCL_DSN ;                                                          
  PDVDD01  = "//DELETEME DD DSN="SCL_DSN",DISP=(OLD,DELETE)"                    
  PDVDD02  = "//SYSEXEC  DD DISP=SHR,DSN="MyCLS2Library                         
                                                                                
  ADDRESS ISPEXEC "FTOPEN TEMP"                                                 
  ADDRESS ISPEXEC "FTINCL ENSP1000"                                             
                                                                                
  ADDRESS ISPEXEC "FTCLOSE "                                                    
                                                                                
  ADDRESS ISPEXEC "VGET (ZUSER ZTEMPF ZTEMPN) ASIS" ;                           
   DEBUG = 'YES' ;                                                              
   DEBUG = 'NAW' ;                                                              
   X = OUTTRAP("OFF")                                                           
   IF DEBUG = 'YES' THEN,                                                       
      DO                                                                        
      ADDRESS ISPEXEC "LMINIT DATAID(DDID) DDNAME(&ZTEMPN)"                     
      ADDRESS ISPEXEC "EDIT DATAID(&DDID)"                                      
      ADDRESS ISPEXEC "LMFREE DATAID(&DDID)"                                    
      END;                                                                      
   ELSE,                                                                        
      DO                                                                        
      PkgSub = "Y"               /* set flag for Package format message */      
      call Submit_n_save_job;                                                   
      END;                                                                      
                                                                                
Increment_jobCard:               /* Increment the QuickEdit Package JobCard */  
  /* Note - there is a bug in quickEdit in that it assumes what it has in       
     sorage (EWB) is correct and it just VPUTS this on exit - so if QE          
     is active on another screen - the job card values will be overwritten      
     with whatever QE thought was a good idea                                   
     */                                                                         
  ADDRESS ISPEXEC "VGET (C1BJC1 C1BJC2 C1BJC3 C1BJC4)" /* get job cards */      
  C1LN = LENGTH(C1BJC1)                                                         
  C1SPACE = INDEX(C1BJC1," ")                                                   
  /*-----------------------------------------------------------------*/         
  /* C1SPACE CONTAINS THE INDEX OF THE FIRST SPACE.  IF THE VALUE IS*/          
  /* IN THE RANGE 4-11 THEN THE JCL STATEMENT IS ASSUMED TO CONTAIN  */         
  /* A VALID JOBCARD.  THE FIRST THREE CHARACTERS OF THE JCL STATE-  */         
  /* MENT ARE ASSUMED TO BE // AND THE FIRST CHARACTER OF THE JOB    */         
  /* NAME.                                                           */         
  /*-----------------------------------------------------------------*/         
  IF (C1SPACE > 3) & (C1SPACE < 12) THEN                                        
    DO                                                                          
      C1LASTCH = SUBSTR(C1BJC1,(C1SPACE - 1),1)                                 
      C1UID    = SUBSTR(C1BJC1,3,(C1SPACE - 4))                                 
      IF  (C1UID = ZUSER) THEN                                                  
        DO                                                                      
          C1CURCH = INDEX("ABCDEFGHIJKLMNOPQRSTUVWXYZA01234567890",C1LASTCH)    
        IF C1CURCH > 0  THEN                                                    
          DO                                                                    
            C1NEXTCH = SUBSTR("ABCDEFGHIJKLMNOPQRSTUVWXYZA01234567890" ,        
                        ,(C1CURCH+1),1)                                         
            C1JCV1 = SUBSTR(C1BJC1,1,(C1SPACE - 2))                             
            C1JCV2 = SUBSTR(C1BJC1,C1SPACE)                                     
            C1BJC1 = C1JCV1 || C1NEXTCH || C1JCV2                               
          END                                                                   
        END                                                                     
    END                                                                         
  ADDRESS ISPEXEC "VPUT (C1BJC1 C1BJC2 C1BJC3 C1BJC4) PROFILE" /* save */       
  return;                                                                       
                                                                                
Sub_Incr_JobCard:                /* This routine either edits (user must        
                                    submit) or submits the job that's been      
                                    built, saving the tso output (jobnum) and   
                                    then increments the job number so that      
                                    the next job will have a unique job num */  
Check_Interpret:                                                                
                                                                                
   /*                                                             */            
   /* Check did the user request to interpret the JOBCARDS?       */            
   /*                                                             */            
                                                                                
   /* as a usability enhancement - allow the user to use regular                
      Endevor Symbols (listed below) instead of having to lookup/               
      the internal symbols name - even if that's easy enough using              
      the UX (dump row contents command)                                        
      */                                                                        
     ENDVSYMS = "C1ENVMNT C1TENVMNT C1EN     C1STGID   C1TSTGID   C1SI    " ,   
                "C1SYSTEM C1TSYSTEM C1SY     C1SUBSYS  C1TSUBSYS  C1SU    " ,   
                "C1ELTYPE C1TELTYPE C1TY     C1LEV     C1TLEV     C1VER   " ,   
            "C1ELEMENT C1TELEMENT C1ELMNT10 C1TELMNT10 C1ELMNT255 C1TELMNT255" ,
                "C1ACTION C1USERID  C1CCID   C1COMMENT C1COM      C1PRGRP ",    
                "C1TPRGRP C1EUDATA"                                             
     LONGSYMS = "EEVETKEN EEVETKEN  EEVETKEN EEVETKSI  EEVETKSI   EEVETKSI" ,   
                "EEVETKSY EEVETKSY  EEVETKSY EEVETKSB  EEVETKSB   EEVETKSB" ,   
                "EEVETKTY EEVETKTY  EEVETKTY EEVETDVL  EEVETDVL   EEVETDVL" ,   
                "LNELMNAM LNELMNAM  LNELMNAM LNELMNAM  EEVETKEL   EEVETKEL " ,  
                "LNLASACT ZUSER     EEVETCCI LNLACOMM  LNLACOMM   EEVETPGR" ,   
                "EEVETPGR USERDATA"                                             
                                                                                
     ADDRESS ISPEXEC "VGET (LNINTERP) profile"                                  
     if JobCard_RC < 8 ,                                                        
      & LNINTERP /= 'Y' then    /* if the user didn't want interpret*/          
        do i = 1 to 8           /* use exactly what they gave us    */          
           SA= value('LNSJC'i,value('LNBJC'i))                                  
        end                                                                     
     else do                   /* otherwise work out what they meant */         
        do i = 1 to 8                         /* for each job card  */          
          savebits = ''                       /* clear accumulator */           
          do j = 1 to length(value('LNBJC'i)) /* to end of line */              
            thisChr = SubStr(value('LNBJC'i),j,1)                               
            if thisChr == '&' then do         /* start of var */                
              k = J                           /* start at name */               
              SymBit = ''                     /* init SymName */                
              do forever                                                        
                k = K + 1 /* inc end of word pointer */                         
                nextChr = SubStr(value('LNBJC'i),k,1)                           
                SymBit = SymBit || nextChr                                      
                /* Getting Fancy!                                               
                   At this point, we can substitute common Endevor              
                   Symbol Names for the internal (row) element names.           
                   But we only want to do this when we see the current          
                   symbol/word is ending... then if it's found in the           
                   lookup table we use the substituted value and carry on       
                   */                                                           
                if pos(SubStr(value('LNBJC'i),(k+1),1),, /* is world ending */  
                   "!@¶$µ&*()_-+={}Ù:;'|\<>,.?/ ") > 0 then do                 
                   EndvNam = wordpos(SymBit,ENDVSYMS) /* lookup symbol name */  
                   if EndvNam > 0 then                                          
                      Symbit = word(LONGSYMS,EndvNam) /* repl*/                 
                end                                                             
                if Symbol(SymBit) == 'VAR' then do /* valid value */            
                /*TmpBit = value(substr(value('LNBJC'i),(j+1),(k-j)))*/         
                  TmpBit = value(SymBit)                                        
                  savebits = savebits || TmpBit                                 
                  j = k                       /* skip over var */               
                  leave                       /* we've substituted this var */  
                end                                                             
                if nextChr == ' ' ,           /* end of current word*/          
                 | nextChr == '&' then do     /* or start new var?  */          
                  savebits = savebits || SubStr(value('LNBJC'i),j,(k-j))        
                  j = k                       /* skip over space */             
                  leave                                                         
                  end                                                           
              end                                                               
            end                                                                 
            else /* not an '&' add it to savebits */                            
            savebits = savebits || thischr                                      
          end                                                                   
          SA=value('LNSJC'i,left(savebits,80))/* save what we found */          
        end                                                                     
      end                                                                       
                                                                                
Do_FTINCL:                                                                      
                                                                                
   EMULTACT = 'YES'             /* signal to use table */                       
   ADDRESS ISPEXEC "VPUT (EMULTACT) SHARED" ;                                   
   ADDRESS ISPEXEC "FTOPEN TEMP"                                                
   ADDRESS ISPEXEC "FTINCL ENDES00L"                                            
   ADDRESS ISPEXEC "FTCLOSE " ;                                                 
   ADDRESS ISPEXEC "VGET (ZUSER ZTEMPF ZTEMPN) ASIS" ;                          
                                                                                
   call clear_Request_queue                 /* all done?  Clean up         */   
                                                                                
Decide_Action:                                                                  
                                                                                
   ADDRESS ISPEXEC "VGET (ENABSUBJ) PROFILE"                                    
   If ENABSUBJ == 'EDIT' then do            /* user want to edit the JCL */     
      ADDRESS ISPEXEC "LMINIT DATAID(DDJC) DDNAME(&ZTEMPN)"                     
      ADDRESS ISPEXEC "VGET (EMULTREQ EMULTGT EMULPRM EMULTYPP) PROFILE"        
      ADDRESS ISPEXEC "SETMSG MSG(LONG022I)"/* Submit or End...          */     
      VARC1LR = ''       /* Allow standard Left/Right  */                       
      ADDRESS ISPEXEC "EDIT   DATAID(&DDJC)",                                   
                      "Profile(JCL)"                                            
      VARC1LR = PASSTHRU /* enable Left/Right commands */                       
      ADDRESS ISPEXEC "LMFREE DATAID(&DDJC)"                                    
      If index(EEVETDMS,'/') > 0 then      /* Did we have a status?     */      
         EEVETDMS = left(EEVETDMS,index(EEVETDMS,'/')) || "Edt'd"               
   end                                                                          
   else do /* Submit the job and save the job name and number           */      
      PkgSub = "N"               /* set flag for Ele Act format message */      
      call Submit_n_save_job                                                    
  END                                                                           
                                                                                
  ADDRESS ISPEXEC "VGET (LNBJC1) PROFILE " /* get latest job card       */      
  C1LN = LENGTH(LNBJC1)                                                         
  C1SPACE = INDEX(LNBJC1," ")                                                   
  /*-----------------------------------------------------------------*/         
  /* C1SPACE CONTAINS THE INDEX OF THE FIRST SPACE.  IF THE VALUE IS*/          
  /* IN THE RANGE 4-11 THEN THE JCL STATEMENT IS ASSUMED TO CONTAIN  */         
  /* A VALID JOBCARD.  THE FIRST THREE CHARACTERS OF THE JCL STATE-  */         
  /* MENT ARE ASSUMED TO BE // AND THE FIRST CHARACTER OF THE JOB    */         
  /* NAME.                                                           */         
  /*-----------------------------------------------------------------*/         
  IF (C1SPACE > 3) & (C1SPACE < 12) THEN                                        
    DO                                                                          
      C1LASTCH = SUBSTR(LNBJC1,(C1SPACE - 1),1)                                 
      C1UID    = SUBSTR(LNBJC1,3,(C1SPACE - 4))                                 
      IF  (C1UID = ZUSER) THEN                                                  
        DO                                                                      
          C1CURCH = INDEX("ABCDEFGHIJKLMNOPQRSTUVWXYZA01234567890",C1LASTCH)    
        IF C1CURCH > 0  THEN                                                    
          DO                                                                    
            C1NEXTCH = SUBSTR("ABCDEFGHIJKLMNOPQRSTUVWXYZA01234567890" ,        
                        ,(C1CURCH+1),1)                                         
            C1JCV1 = SUBSTR(LNBJC1,1,(C1SPACE - 2))                             
            C1JCV2 = SUBSTR(LNBJC1,C1SPACE)                                     
            LNBJC1 = C1JCV1 || C1NEXTCH || C1JCV2                               
          END                                                                   
        END                                                                     
    END                                                                         
  ADDRESS ISPEXEC "VPUT (LNBJC1) PROFILE "                                      
                                                                                
Clear_Request_Count:                                                            
   EMULTREQ = 0                             /* reset count of requests     */   
   EMULTCNT = 0                             /* reset count of requests     */   
   EMULTGT  = ""                            /* reset count of requests     */   
   EMULPRM  = ""                            /* reset count of requests     */   
   EMULTYPP = ""                            /* reset array of types        */   
   EMULTYPS = ""                            /* reset array of types        */   
   ADDRESS ISPEXEC "VPUT (EMULTREQ EMULTCNT EMULTGT EMULPRM EMULTYPP) PROFILE"  
   ADDRESS ISPEXEC "VPUT (EMULTYPS) SHARED"                                     
                                                                                
  RETURN ;                                                                      
                                                                                
Submit_n_save_job: /* submit current ZTEMPF job and save job info       */      
   Address TSO "PROFILE NOINTERCOM"     /* turn off msg notific      */         
   CALL MSG "ON"                                                                
   CALL OUTTRAP "out."                                                          
   ADDRESS TSO "SUBMIT '"ZTEMPF"'" ;                                            
   CALL OUTTRAP "OFF"                                                           
   DO k = 1 TO out.0                                                            
   /*                                                                */         
   /* Added support for up to 7-digit job names (only seen 6)        */         
   /*                                                                */         
      PARSE VAR out.k WITH . "JOB " jobnam "(J" JOBNUM ")" .                    
      if left(jobnum,2) == 'OB' then    /* If we have .(JOBnnnnn)    */         
        jobnum = substr(jobnum,3)                                               
      else                                                                      
        if left(jobnum,1) == 'O' then   /* or we have .(JOnnnnnn)    */         
          jobnum = substr(jobnum,2)                                             
      if jobnum == '' then jobnum = '*' /* default to last fnd       */         
   END                                                                          
   ZEDSMSG = jobnam || '(' || jobnum || ') Sub''d'                              
   if PkgSub = "Y" then /* format the history message for package or ele */     
      SUBHIST = left(Date(N),06,'00'x)copies('00'x,1)left(Time(N),09,'00'x),    
        ||Left('Package',09,'00'x)LEFT('Actions',09,'00'x),                     
        ||Left(ZEDSMSG,16,'00'x)LEFT(PACKAGE,27,'00'x)|| SUBHIST                
   else                                                                         
      SUBHIST = left(Date(N),06,'00'x)copies('00'x,1)left(Time(N),09,'00'x),    
        ||Left(LNElmNam,09,'00'x)LEFT(LNLASACT,09,'00'x),                       
        ||Left(ZEDSMSG,16,'00'x)LEFT(EEVETKEL,27,'00'x)|| SUBHIST               
   ZEDLMSG = left('Submitted Jobs History (This Session)',77,'00'x)||SUBHIST    
   ADDRESS ISPEXEC "VPUT (SUBHIST) SHARED"  /* Update history value  */         
   ADDRESS ISPEXEC "SETMSG MSG(ISRZ000)"                                        
   Address TSO "PROFILE INTERCOM"       /* restore msg notific       */         
   If index(EEVETDMS,'/') > 0 then      /* Did we have a status?     */         
      EEVETDMS = left(EEVETDMS,index(EEVETDMS,'/')) || "Sub'd"                  
                                                                                
  RETURN ;                                                                      
                                                                                
Clear_Request_queue:                                                            
   ADDRESS ISPEXEC "TBTOP      EMULTTAB"    /* Because we could be split...*/   
   DO until rc > 0                          /* Clear out submitted records */   
     ADDRESS ISPEXEC "TBSKIP   EMULTTAB"    /* incase there is a split     */   
     ADDRESS ISPEXEC "TBDELETE EMULTTAB"    /* screen user active...       */   
   END                                      /* Then close the table for    */   
   ADDRESS ISPEXEC "TBSTATS  EMULTTAB ROWCURR(EMULTCNT)" /* how many rows?  */  
   ADDRESS ISPEXEC "VPUT (EMULTCNT) PROFILE"/* save it for LongSubJ panel   */  
   ADDRESS ISPEXEC "TBCLOSE EMULTTAB"       /* this screen                 */   
                                                                                
   Return                                                                       
                                                                                
Summary_Of_Levels:                                                              
/* Uncomment the following lines to debug the summary report                    
   ADDRESS ISPEXEC "LMINIT DATAID(DDSU) DDNAME("LONGDDPR"BROWS)"                
   ADDRESS ISPEXEC "EDIT   DATAID(&DDSU)"                                       
   ADDRESS ISPEXEC "LMFREE DATAID(&DDSU)"                                       
   */                                                                           
                                                                                
  SELS = '' ;                                                                   
  chkheadf = 'NO'                /* set flag off at first */                    
                                                                                
  "EXECIO * DISKR "LONGDDPR"BROWS (STEM SUM. finis"                             
  /* "FREE F("LONGDDPR"BROWS)" */                                               
                                                                                
  Call CREATE_SummaryLevels_Table;                                              
/*                                                                              
  In the summary of levels report there are two sets of summary lines           
  the first set shows the SYNC CCID and COMMENT, the second set is the one      
  with the number of inserted and deleted lines.  So need to first add,         
  and then update the table with the 2nd set of data                            
  */                                                                            
  SUMMSG  = ''                           /* Initialise to spaces */             
  USER    = '???????'                    /* establish defaults   */             
  SUMDATE = 'DDMMMYY'                    /* for first pass */                   
  SUMTIME = 'HH:MM'                                                             
  STMTS   = 'sss'                                                               
  INSERTS = 'iii'                                                               
  DELETES = 'ddd'                                                               
  Do rec# = 1 to SUM.0                                                          
     IF Words(SUM.rec#) < 5 then iterate;                                       
     if chkheadf == 'NO' then do         /* have we found 2nd head yet?*/       
       chkheadi     = Word(SUM.rec#,6) ; /* looking for INSERTS */              
       chkheadd     = Word(SUM.rec#,7) ; /* looking for DELETES */              
       if chkheadi == 'INSERTS' ,                                               
        & chkheadd == 'DELETES' THEN DO                                         
          chkheadf = 'YES'               /* save the flag to start looking */   
          iterate                        /* and get next record */              
       end                                                                      
     end                                                                        
     EEVETDVL     = Word(SUM.rec#,1) ;   /* grab first word       */            
     if length(EEVETDVL) = 5,            /* Check for old vv.ll   */            
      & Substr(EEVETDVL,3,1) == '.' then /* is there a dot too?   */            
        EEVETDVL = left(EEVETDVL,2) || right(EEVETDVL,2) ;                      
     If Length(EEVETDVL) /= 4 then iterate ; /* we expect vvll now */           
     If EEVETDVL = "VVLL" then do        /* Parse the header */                 
        POSSYNC = index(SUM.rec#,'SYNC') /* Note the offset for SYNC */         
        POSCCID = index(SUM.rec#,'CCID') /* CCID and comment */                 
        POSCOMM = index(SUM.rec#,'COMMENT')                                     
        iterate                          /* then get the next record */         
     end                                                                        
     iF DATATYPE(EEVETDVL,'W') \= 1 then iterate /* only whole numbers */       
     /* at this point we have a record to store check our flag */               
     if chkheadf == 'NO' then do         /* we're still on first pass */        
        SUMSYNC = strip(substr(SUM.rec#,POSSYNC,4)) /* grab SYNC flag */        
        SUMCCID = strip(substr(SUM.rec#,POSCCID,12),t) /* grab CCID  */         
        SUMCOMM = strip(substr(SUM.rec#,POSCOMM,40),t) /* grab COMM  */         
        ADDRESS ISPEXEC "TBADD "LONGTABS" ORDER ";     /* save it    */         
        iterate                                        /* get next   */         
     end                                                                        
     else do                             /* 2nd pass */                         
        ADDRESS ISPEXEC "TBGET "LONGTABS; /* reposition */                      
        USER         = Word(SUM.rec#,2) ;                                       
        SUMDATE      = Word(SUM.rec#,3) ;                                       
        SUMTIME      = Word(SUM.rec#,4) ;                                       
        STMTS        = Word(SUM.rec#,5) ;                                       
        INSERTS      = Word(SUM.rec#,6) ;                                       
        DELETES      = Word(SUM.rec#,7) ;                                       
        ADDRESS ISPEXEC "TBPUT "LONGTABS; /*update it */                        
     end                                                                        
                                                                                
  End; /* Do rec# = 1 to SUM.0 */                                               
                                                                                
  Call Display_SummaryLevels_Table ;                                            
                                                                                
  Return                                                                        
                                                                                
CREATE_SummaryLevels_Table:                                                     
                                                                                
  SA= 'CREATE_SummaryLevels_Table               ' ;                             
  SA= "CREATE_SummaryLevels_Table "LONGTABS                                     
  ADDRESS ISPEXEC,                                                              
    "TBCREATE "LONGTABS" REPLACE SHARE" ,                                       
    "KEYS(EEVETKEL EEVETKEN EEVETKSY EEVETKSB" ,                                
         "EEVETKTY EEVETKSI EEVETKST EEVETKSN" ,                                
         "EEVETDSL EEVETDVL)",                                                  
    "NAMES(SUMMSG USER SUMDATE SUMTIME STMTS INSERTS DELETES",                  
          "SUMCCID SUMCOMM SUMSYNC",                                            
          "LNLACOMM LNDESCRP LNELMNAM LNLASACT",                                
          "LNLADATE LNLATIME LNUPDATE LNUPTIME",                                
          "LNMVDATE LNMVTIME LNGEDATE LNGETIME",                                
          "LNDPKGIS LNDPKGIO LNDPKGDO LNDPKGBO",                                
          "EEVETDMS EEVETNS  EEVETPGR EEVETUID",                                
          "EEVETCCI EEVETPRC EEVETNRC EEVETSO ",                                
          "LNDPKGLK LNLKDATE LNLKTIME LNLOCKED",                                
          "USERDATA LNCSVSEQ LNDGCCID LNDRCCID) NOWRITE " ;                     
                                                                                
  RETURN;                                                                       
                                                                                
Display_SummaryLevels_Table:                                                    
                                                                                
   ADDRESS ISPEXEC "TBSORT "LONGTABS,                                           
           "FIELDS(EEVETDVL,C,A) ";                                             
   SA= 'COMPLETED TBSORT  ' ;                                                   
   ADDRESS ISPEXEC "TBTOP   "LONGTABS ;                                         
   ADDRESS ISPEXEC "TBDISPL "LONGTABS" PANEL("LONGPANS")"                       
   IF RC > 4 THEN,                                                              
      DO                                                                        
      ADDRESS ISPEXEC "TBEND "LONGTABS         ;                                
      return                                                                    
      end;                                                                      
   TBDISPL_RC = RC ;                                                            
   SUMVTOPR = ZTDTOP      /* save the top row so we can restore it */           
/* DO WHILE ZTDSELS > 0  */                                                     
   DO FOREVER                                                                   
      SA= 'ZTDSELS=' ZTDSELS                                                    
      SELS = Strip(SELSMLV)                                                     
      Selected_EEVETDVL = EEVETDVL;                                             
      SRCTYP = ""                       /* Default to EBCDIC               */   
/*    IF Strip(SELSMLV) = "S" then SA= "Selected" EEVETKEL ; */                 
      IF index('BCHM',SELS) > 0 then    /* Browse, Changes, History or Master*/ 
         Do                                                                     
         "ISPEXEC CONTROL DISPLAY SAVE"                                         
         Call Browse_Element_TSO;                                               
         "ISPEXEC CONTROL DISPLAY RESTORE"                                      
         SUMMSG = EEVETDMS              /* use message from browse routine */   
         End                                                                    
      IF SELS == "V" then,                                                      
         Do                                                                     
         SUMMSG = "*Viewed"                                                     
         "ISPEXEC CONTROL DISPLAY SAVE"                                         
         Call View_Element_TSO;                                                 
         "ISPEXEC CONTROL DISPLAY RESTORE"                                      
         End                                                                    
      IF SELS == "E" then,                                                      
         Do                                                                     
         SUMMSG = "*Edited"                                                     
         "ISPEXEC CONTROL DISPLAY SAVE"                                         
         Call Retrieve_Element_TSO                                              
         Call Edit_Element_TSO;                                                 
         "ISPEXEC CONTROL DISPLAY RESTORE"                                      
         End                                                                    
      IF SELS == "EA" then,                                                     
         Do                                                                     
         SUMMSG = "*Edited"                                                     
         SRCTYP = "ASCII"                  /* Special treatment for ASCII */    
         "ISPEXEC CONTROL DISPLAY SAVE"                                         
         Call Retrieve_Element_TSO                                              
         Call Edit_Element_TSO;                                                 
         "ISPEXEC CONTROL DISPLAY RESTORE"                                      
         End                                                                    
      IF SELS == "EU" then,                                                     
         Do                                                                     
         SUMMSG = "*Edited"                                                     
         SRCTYP = "UTF8"                   /* Special treatment for Unicode */  
         "ISPEXEC CONTROL DISPLAY SAVE"                                         
         Call Retrieve_Element_TSO                                              
         Call Edit_Element_TSO;                                                 
         "ISPEXEC CONTROL DISPLAY RESTORE"                                      
         End                                                                    
      IF (left(SELS,1)) = "U" ,                                                 
       | (left(SELS,1)) = "D"  then do                                          
         Call User_Routine; /* Call User or Diff routine */                     
         SUMMSG = USERMSG   /* set the messate to the Routine value */          
         end                                                                    
      SELS    = '' ;                                                            
      SELSMLV = '' ;                                                            
      if ZTDSELS > 0 then    /* if there was anything to process save it? */    
         ADDRESS ISPEXEC "TBPUT   "LONGTABS         ;                           
      if ZTDSELS > 1 then do /* if there are more pending rows to display? */   
         ADDRESS ISPEXEC "TBDISPL "LONGTABS                                     
         TBDISPL_RC = RC ;                                                      
         iterate                                                                
         end                                                                    
      if TBDISPL_RC >= 8 then leave;                                            
      THISCMD = ZCMD;                /* get any command */                      
      ZCMD = "";                     /* reset the command */                    
      parse upper var THISCMD THISCMDW THISCMDP                                 
                                                                                
      select                                                                    
        when THISCMDW == ''              then NOP;                              
        when THISCMDW == 'LEFT'          then call Toggle_Sum_Screen;           
        when THISCMDW == 'RIGHT'         then call Toggle_Sum_Screen;           
      otherwise                                                                 
        do                                                                      
           ZCMD = THISCMD /* restore command so it can be corrected */          
           ADDRESS ISPEXEC "SETMSG MSG(LONG013E)" ; /* Cmd Not Recognized */    
        end                                                                     
      end                                                                       
      ADDRESS ISPEXEC "TBTOP   "LONGTABS ;                                      
      ADDRESS ISPEXEC "TBSKIP  "LONGTABS" NUMBER("SUMVTOPR")"                   
      ADDRESS ISPEXEC "TBDISPL "LONGTABS" PANEL("LONGPANS")"                    
      TBDISPL_RC = RC ;                                                         
      If TBDISPL_RC >= 8  then,                                                 
         leave                                                                  
      SUMVTOPR = ZTDTOP      /* save the top row so we can restore it */        
   END; /* Do until user selects END or RETURN */                               
                                                                                
   Selected_EEVETDVL = " " ;                                                    
                                                                                
   ADDRESS ISPEXEC "TBEND "LONGTABS ;                                           
                                                                                
   return ;                                                                     
                                                                                
User_Routine:                                                                   
   UsrRtnUx = 'USRRTN'SELS                   /* One of U* D* or P* for:   */    
   SA= 'Calling User Routine' USRRTNUX       /* User, Diff, or PDM        */    
   ADDRESS ISPEXEC "TBQUERY "CURRTABL" KEYS(TABKEYS) NAMES(TABVARS)" ,          
                   "POSITION(USERROW)"       /* Get & pass Table Info,    */    
   USERCMD  = SELS                           /* Pass the user command     */    
   USERMSG  = '*' || UsrRtnUx                /* Set up a default message, */    
   USERTABL = CURRTABL                       /* With he table name and CRP*/    
   passing = 'USERCMD USERMSG USERTABL USERROW' , /* collect all the var */     
              STRIP(SUBSTR(TABKEYS,2),,')'), /* Stripping brackets so that*/    
              STRIP(SUBSTR(TABVARS,2),,')')  /* the routine can get them. */    
   passing = 'PASSING' passing               /* prepend the name of names */    
   ADDRESS ISPEXEC "VPUT ("passing") SHARED" /* Save vars for User Routine*/    
   ADDRESS ISPEXEC "CONTROL DISPLAY SAVE"    /* Save the display status   */    
   ADDRESS ISPEXEC "CONTROL RETURN ERRORS"   /* Trap any errors           */    
   ADDRESS ISPEXEC,                          /* Then call the user routine*/    
        "SELECT CMD("UsrRtnUx "'PASSING')"   /* passing the variable names*/    
   ADDRESS ISPEXEC "VGET (USERMSG) SHARED"   /* After calling the routine */    
   ADDRESS ISPEXEC "CONTROL ERRORS CANCEL"   /* ...get the message back,  */    
   ADDRESS ISPEXEC "CONTROL DISPLAY RESTORE" /* restore errors and panel  */    
   ADDRESS ISPEXEC "TBTOP   "CURRTABL        /* Reposition table          */    
   ADDRESS ISPEXEC "TBSKIP  "CURRTABL " NUMBER("USERROW")" /* curr row... */    
   ADDRESS ISPEXEC "TBGET   "CURRTABL        /* get it for update         */    
   EEVETDMS = USERMSG                        /* and set the value to the  */    
                                             /* message passed back,      */    
                                             /* But there could be more...*/    
   ADDRESS ISPEXEC "VGET (USERDATA) SHARED"  /* For example the UserData  */    
   RETURN;                                                                      
                                                                                
/****************************************************************************/  
                                                                                
SaveScl : procedure  expose SCLLINE. LNLASTGT LNLASPRM LNLASTYP,                
                     EMULTREQ EMULTGT EMULPRM EMULTYPP EMULTYPS                 
                                     /* This routine handles accumulating       
                                        SCL lines until it's time to pass       
                                        them off to the execute routine         
                                                                                
                                        Eventually it might handle auto         
                                        formatting long lines but at first      
                                        the goal is just to save each line      
                                        passed and increment the counter.       
                                        */                                      
if ARG(1) == 'RESET' then do         /* Reset request?                      */  
   SCLLINE. = ''                     /* reset stem var                      */  
   SCLLINE.0 = 0                     /* and counter                         */  
   return SCLLINE.0                  /* normaly return the number of lines  */  
end                                                                             
                                                                                
if ARG(1) == 'EXECIO' then do        /* We need to write our lines to arg(2)*/  
   OUTDD = ARG(2)                    /* reset Output                        */  
   "EXECIO * DISKW" OUTDD "(STEM SCLLINE. FINIS" /* write all output        */  
   return RC                         /* Return The RC in this case          */  
end                                                                             
                                                                                
if ARG(1) == 'GETALL' then do        /* Return all SCL                      */  
   ALLSCL = ''                       /* reset Output                        */  
   do I = 1 to SCLLINE.0             /* For each saved line                 */  
     ALLSCL = ALLSCL || LEFT(SCLLINE.i,80) /* append the SCL line           */  
   end                                                                          
   return ALLSCL                     /* Return all the SCL as a single str  */  
end                                                                             
                                                                                
if ARG(1) == 'ADD2TAB' then do       /* Append SCL to Table, for FTINCL     */  
                                                                                
   /* Any active requests?        */                                            
   ADDRESS ISPEXEC "VGET (EMULTREQ EMULTGT EMULPRM EMULTYPP" ,                  
                         ") PROFILE" /* get saved details                   */  
   if SCLLINE.0 > 0 then do          /* Are there any requests to store?    */  
      ADDRESS ISPEXEC    "TBSTATS  EMULTTAB STATUS2(Stat2RC)"                   
      If Stat2RC = 1 then            /* table not open yet                  */  
         ADDRESS ISPEXEC "TBOPEN   EMULTTAB SHARE NOWRITE"                      
      If rc =  8 Then do             /* No Table?  - Create one             */  
         ADDRESS ISPEXEC "TBCREATE EMULTTAB NAMES(SCLLINE) SHARE NOWRITE"       
         If rc ^= 0 Then             /* Return codes                        */  
            Say "Table Create Error:"RC "Req:"EMULTREQ                          
         EMULTREQ = 0                /* reset count of requests             */  
         EMULTGT  = ""               /* Reset action Environment            */  
         EMULPRM  = ""               /* Reset action                        */  
      END                                                                       
      else ADDRESS ISPEXEC "TBBOTTOM EMULTTAB" /* go to bottom              */  
      EMULTREQ = EMULTREQ + 1        /* Increment count of requests         */  
      iF EMULTGT == "" THEN          /* First time?                         */  
         EMULTGT  = LNLASTGT         /* just save it                        */  
      ELSE                           /* otherwise...                        */  
         if EMULTGT \= LNLASTGT THEN /* Is this request the same as last?   */  
            EMULTGT  = "@"           /* Flag as Multi- target stage         */  
      iF EMULPRM  = "" THEN          /* First time?                         */  
         EMULPRM  = LNLASPRM         /* just save promote eleigble flag     */  
      ELSE                           /* otherwise...                        */  
         if EMULPRM \= LNLASPRM THEN /* Is this request the same as last?   */  
            EMULPRM  = "N"           /* Flag as Multi- target stage         */  
      if wordpos(LNLASTYP,EMULTYPP) = 0 then /* have we got this type yet? */   
         EMULTYPP = EMULTYPP LNLASTYP /* save this type                    */   
      ADDRESS ISPEXEC "VPUT (EMULTREQ EMULTGT EMULPRM EMULTYPP" ,               
                            ") PROFILE" /* and save 'em                     */  
      EMULTYPS = strip(EMULTYPP)     /* Shared profile var for Skeleton     */  
      ADDRESS ISPEXEC "VPUT (EMULTYPS) SHARED"                                  
      SCLLINE = '* Request' || right(EMULTREQ,4) /* build a hearder line    */  
      ADDRESS ISPEXEC "TBADD EMULTTAB"  /* Add a request divider/header line*/  
      do I = 1 to SCLLINE.0             /* For each saved line              */  
         SCLLINE = LEFT(SCLLINE.i,80)   /* Grab the SCL line                */  
         ADDRESS ISPEXEC "TBADD EMULTTAB" /* Add this line                  */  
      end                                                                       
   end                                                                          
   ADDRESS ISPEXEC "TBSTATS  EMULTTAB ROWCURR(EMULTCNT)" /* how many rows?  */  
   ADDRESS ISPEXEC "VPUT (EMULTCNT) PROFILE"/* save it for LongSubJ panel   */  
   return EMULTREQ                   /* Return current request count        */  
end                                                                             
        /* still here?  Must have some SCL to save... */                        
        /* But if we're runing Endevor v18 with spaces in an element name   */  
        /* we may need to save the scl in the new "space safe" syntax       */  
   p = SCLLINE.0                     /* how many lines saved?               */  
   if P > 0 ,                        /* If we have at least one line,       */  
    & left(ARG(1),1) == "'" ,        /* does this line start with quote?    */  
    & pos(' ',substr(ARG(1),2,(length(ARG(1))-2))) > 1 , /* and fnd a space */  
    & pos('ELEMENT',SCLLINE.p) > 0 then do /* prev scl line said element?   */  
      sa= "We found a spage age ele:" Arg(1)  /* debug v18 syntax reqd      */  
      do j = 2  by 50 while j < length(ARG(1))                                  
         i = SCLLINE.0 + 1              /* increment the line count         */  
         SCLLINE.i = "     '" || substr(ARG(1),j,50) || "'," /* next chunk  */  
         SCLLINE.0 = i                  /* and the new count                */  
      end                                                                       
      /* but the scl already had a closing quote so we need to clean up...  */  
      SCLLINE.i = strip(SCLLINE.i,T,",") /* strip trailing comma            */  
      SCLLINE.i = strip(SCLLINE.i,T,"'") /* and trailing apost              */  
   end                                                                          
   else /* a normal SCL line might still be long...                         */  
      do j = 1  by 72 while j < length(ARG(1))                                  
         i = SCLLINE.0 + 1              /* increment the line count         */  
         SCLLINE.i = substr(ARG(1),j,72)/* save next chunk                  */  
         SCLLINE.0 = i                  /* and the new count                */  
      end                                                                       
   return SCLLINE.0                  /* Return the number of lines todate   */  
                                                                                
/****************************************************************************/  
                                                                                
FindMsg : procedure  expose FMsg.    /* This routine handles finding a          
                                        message to the appropriate RC:          
                                        It takes to arguments the DD to search  
                                        and the expected RC to search for       
                                        It will return the corresponding        
                                        Q/E ISPF message if known               
                                        RC=0 - Finds Info Messages              
                                        RC=4 - Finds Warning Messages           
                                        RC=8 - Finds Caution Messages           
                                        RC=12- Finds Error Messages             
                                        RC=20- Finds Severe Messages            
                                        RC=99- Finds the highest message        
                                        */                                      
/* check C1MSGS for Add to Env/Sys/Stage... */                                  
   "EXECIO * DISKR" ARG(1) "(STEM MSA. finis"                                   
/* append these messages to C1MSGS1 so that user can see them too */            
   "EXECIO * DISKW C1MSGS1 (STEM MSA. FINIS" /* append messages         */      
   FMsg. = ''                                                                   
   FMsg.lvl = 0                                                                 
                                                                                
   Do rec# = 1 to MSA.0                                                         
      /*  05:46:08  IMGR013W  NO TYPES MATCH ZAVA IN SYSTEM CONCURNT - STAGE **/
      parse value MSA.rec# with 12 thismsg 20 19 thissev 20 22 thistxt 133      
      select                                                                    
        when thissev == "I" then thislvl = 0                                    
        when thissev == "W" then thislvl = 4                                    
        when thissev == "C" then thislvl = 8                                    
        when thissev == "E" then thislvl = 12                                   
        when thissev == "S" then thislvl = 20                                   
        otherwise thislvl = 0                                                   
     end                                                                        
     if thislvl > FMsg.lvl then do                                              
        FMsg.lvl = thislvl                                                      
        FMsg.msg = thismsg                                                      
        FMsg.txt = thistxt                                                      
        FMsg.sev = thissev                                                      
        if FMsg.lvl >= ARG(2) then leave                                        
        end                                                                     
   end                                                                          
   a =          wordpos(FMsg.msg,'IMGR005W IMGR007W IMGR013W IMGR009W')         
   if a > 0 then FMsg.isp = word('ENDE005E ENDE006E ENDE007E ENDE019E',a)       
   return FMsg.isp                                                              
exit 999                             /* Should never hit this               */  
/* CWORD & CWORDS are functiona to work like Words and word, but using          
            comma delimited words instead of space delimited words              
            unlike words however adjacent commas will not be treated            
            as a single delimiter (to make more sense of CSV data)              
            but I can imagine instance when this might be required              
            additionally the CWORD functin will strip leading and               
            trailing space(s) so that it will operate more like WORD(n)         
            in this case.                                                       
*/                                                                              
                                                                                
CWords:procedure                                                                
   parse arg inword                                                             
   if length(inword) = 0 then return 0                                          
   count = 1                                                                    
   do i = 1 by 1 to length(inword)                                              
     if substr(inword,i,1) == ',' then                                          
        count = count + 1                                                       
   end                                                                          
   return count                                                                 
                                                                                
CWord:procedure                                                                 
   parse arg inword, incount                                                    
   sa= "inword:'" || inword || "' and count:" incount                           
   outword = ''                                                                 
   if incount = 0,                                                              
    | incount > cwords(inword) then return outword                              
   count = 1                                                                    
   do i = 1 by 1 while count < incount                                          
     if substr(inword,i,1) == ',' then                                          
        count = count + 1                                                       
   end                                                                          
   do j = I by 1 until j >= length(inword)                                      
     thisChr = substr(inword,j,1)                                               
     if thisChr == ',' then leave                                               
     outword = Outword||thischr                                                 
   end                                                                          
   return strip(outword)                                                        
                                                                                
/*                                                                              
   DWords for "delimited words" is a slightly more complex version of CWords    
   (comma delimited words) it check to see if the words are also wrapped in     
   quotes in addition to a delimiter of "º" and if so, it also stripps them.    
   This allows it to work more reliably with Endevor's CSV outpu, but it's      
   still not guaranteed against folks adding double.quoteºdouble.quote into     
   a text field.  Long term Endevor's CSV should probably more closely match    
   EXCEL's standard and or allow escape! when a delimiter is found in the data  
                                                                                
   */                                                                           
DWords:procedure                                                                
   parse arg inword                                                             
   if length(inword) = 0 then return 0                                          
   select                       /* check for Quote or Apost delimiters   */     
     when left(inword,1) == '"' then TgtStr = '"º"'                             
     when left(inword,1) == "'" then TgtStr = "'º'"                             
     othersise                  then TgtStr =  "º"                              
   end                                                                          
   lenTgt = length(TgtStr)      /* if extra delim's used extend comp len */     
   count = 1                                                                    
   do i = 1 by 1 to length(inword)                                              
     if substr(inword,i,lenTgt) == TgtStr then                                  
        count = count + 1                                                       
   end                                                                          
   return count                                                                 
                                                                                
DWord:procedure                                                                 
   parse arg inword, incount                                                    
   sa= "inword:'" || inword || "' and count:" incount                           
   select                       /* check for Quote or Apost delimiters   */     
     when left(inword,1) == '"' then TgtStr = '"º"'                             
     when left(inword,1) == "'" then TgtStr = "'º'"                             
     othersise                  then TgtStr =  "º"                              
   end                                                                          
   lenTgt = length(TgtStr)      /* if extra delim's used extend comp len */     
   if lenTgt > 1 then           /* Calculate an offset depending on lenth*/     
      if incount = 1 then offset = 1 /* the First word will only have one*/     
      else                offset = 2 /* otherwise we have to skip both   */     
   else                   offset = 0 /* but if no delim's then ignore    */     
   outword = ''                                                                 
   if incount = 0,                                                              
    | incount > dwords(inword) then return outword                              
   count = 1                    /* start by skiping to end of prev word  */     
   do i = 1 by 1 while count < incount                                          
     if substr(inword,i,lenTgt) == TgtStr then                                  
        count = count + 1                                                       
   end                                                                          
   do j = I+offset by 1 until j >= length(inword) /* start with offset   */     
     thisChr = substr(inword,j,lenTgt)                                          
     if thisChr == TgtStr then leave              /* get out if next fnd */     
     if j = length(inword) then                   /* or we find trailing */     
        if substr(inword,j,1) == left(TgtStr,1) then leave /* delimiter  */     
     outword = Outword||left(thischr,1)                                         
   end                                                                          
   return strip(outword)                                                        
